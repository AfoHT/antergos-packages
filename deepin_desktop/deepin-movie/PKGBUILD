# Maintainer: Steve Ovens <steve_ovens [AT] linux DOT com>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-movie
pkgver=2.2.2.20150616111111
# TODO
_srcdirname=deepin-movie
# _srcdirname=deepin-movie-2.2.2+git20150616111111~0445b03b5f
pkgrel=1
pkgdesc='deepin movie for linuxdeepin desktop environment'
arch=('any')
url="https://github.com/linuxdeepin/deepin-movie"
license=('GPL3')
makedepends=('python2-sip' 'deepin-gettext-tools')
groups=('deepin-extra')
install="${pkgname}.install"
source=("http://ftp5.gwdg.de/pub/linux/linuxdeepin/packages/pool/main/d/deepin-movie/deepin-movie_2.2.2+git20150616111111~0445b03b5f.tar.gz")
sha256sums=('827d222167bac6f98671added1fa93a642ac9302a599fe732b4b3983530651de')

prepare() {
  cd "${_srcdirname}"

  sed 's\python\python2\' -i Makefile

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd "${_srcdirname}"

  make
}

package() {
  depends=('qt5-declarative' 'qt5-quickcontrols' 'qt5-multimedia' 'qt5-webkit'
           'qt5-graphicaleffects' 'qt5-imageformats' 'qt5-systems'
           'python2-ass' 'python2-magic' 'python2-peewee' 'python2-pyqt5'
           'python2-pysrt' 'python2-setproctitle' 'xpybutil'
           'dde-go-dbus-factory' 'deepin-menu' 'deepin-qml-widgets' 'python2-deepin-ui'
           'desktop-file-utils' 'enca' 'mediainfo' 'qtav')

  cd "${_srcdirname}"

  make DESTDIR="${pkgdir}" install
}
