# Maintainer: Steve Ovens <steve_ovens [AT] linux DOT com>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-movie
pkgver=2.2.1.20150512143336
_srcdirname=deepin-movie-2.2.1+20150512143336~131ab9d6c9
pkgrel=2
pkgdesc='deepin movie for linuxdeepin desktop environment'
arch=('any')
url="https://github.com/linuxdeepin/deepin-movie"
license=('GPL3')
makedepends=('python2-sip' 'deepin-gettext-tools')
groups=('deepin-extra')
install="${pkgname}.install"
source=("http://deepin.mirror.gtcomm.net/pool/main/d/deepin-movie/deepin-movie_2.2.1+20150512143336~131ab9d6c9.tar.gz")
sha256sums=('8e9e1cb3f099863ecb8949b3445320ad6c1a1c94f98852848d04abef16d05237')

prepare() {
  cd "${_srcdirname}"

  sed 's\python\python2\' -i Makefile

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd "${_srcdirname}"

  make
}

package() {
  depends=('qt5-declarative' 'qt5-quickcontrols' 'qt5-multimedia' 'qt5-webkit'
           'qt5-graphicaleffects' 'qt5-imageformats' 'qt5-systems'
           'python2-ass' 'python2-magic' 'python2-peewee' 'python2-pyqt5'
           'python2-pysrt' 'python2-setproctitle' 'xpybutil'
           'dde-go-dbus-factory' 'deepin-menu' 'deepin-qml-widgets' 'python2-deepin-ui'
           'desktop-file-utils' 'enca' 'mediainfo' 'qtav')

  cd "${_srcdirname}"

  make DESTDIR="${pkgdir}" install
}
