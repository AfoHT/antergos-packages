# Maintainer: Steve Ovens <steve_ovens [AT] linux DOT com>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Piotr Husiaty≈Ñski <phusiatynski@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Leios <jrs0037@auburn.edu>

pkgname=python2-xpyb
_pkgname=xpyb
pkgver=1.3.1
pkgrel=1
pkgdesc="X Python Binding, based on the XCB library"
url=http://xcb.freedesktop.org/
groups=("deepin" "deepin-base")
arch=('i686' 'x86_64')
license=('custom:public')
depends=('python2' 'libxcb')
options=(!libtool)
provides=('python2-xcffib')
conflicts=('python2-xcffib')
source=(http://xcb.freedesktop.org/dist/${_pkgname}-${pkgver}.tar.bz2
        64.patch)
md5sums=('b9b70746cd348836516edcba96d24677'
         '166e5bdf37b7b127dbf53493a330b764')

build() {
  cd "$srcdir/$_pkgname-$pkgver/src"
  patch -p1 -i "$srcdir"/64.patch
  cd ..
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr
  make
}
package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$_pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$_pkgname/"
}
