# Maintainer: Steve Ovens <steve_ovens [AT] linux DOT com>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=qtav
pkgver=1.7.0.20150711.68322f8
pkgrel=1
pkgdesc="qt multimedia framework"
arch=('i686' 'x86_64')
url="https://github.com/wang-bin/QtAV"
groups=("deepin" "deepin-base")
license=('GPL3' 'LGPL2.1')
depends=('desktop-file-utils'
		 'ffmpeg'
		 'hicolor-icon-theme'
		 'libxv'
		 'qt5-declarative'
		 'qt5-svg')
makedepends=('openal' 'portaudio' 'qt5-tools')
optdepends=('openal'
			'portaudio')
install=qtav.install
source=("${pkgname}-${pkgver}.tar.xz"
		'libchardet-git.tar.xz'
		'capi-git.tar.xz')
sha256sums=('SKIP' 'SKIP' 'SKIP')

prepare() {
	cd "${pkgname}-${pkgver}"

	rm -rf contrib/{libchardet,capi}
	cp -r "${srcdir}/libchardet-git" "contrib/libchardet"
	cp -r "${srcdir}/capi-git" "contrib/capi"
}

build() {
	cd "${pkgname}-${pkgver}"

	mkdir build && cd build
	qmake-qt5 "CONFIG+=no_rpath recheck" ../QtAV.pro
    make -j8 PREFIX=/usr
}

check() {
	cd "${pkgname}-${pkgver}"/build

    make check
}

package() {
	cd "${pkgname}-${pkgver}"/build

	make install DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}"

	# Useful symlinks
  	install -d "${pkgdir}"/usr/bin
  	ln -s /usr/lib/qt/bin/player "${pkgdir}"/usr/bin/qtav-player
	ln -s /usr/lib/qt/bin/QMLPlayer "${pkgdir}"/usr/bin/QMLPlayer

	install -D ../src/QtAV.svg "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/QtAV.svg

	mv "${pkgdir}"/usr/share/applications/player.desktop "${pkgdir}"/usr/share/applications/qtav-player.desktop
	sed -i'' '8,9 s/player/qtav-player/' "${pkgdir}"/usr/share/applications/qtav-player.desktop
}
