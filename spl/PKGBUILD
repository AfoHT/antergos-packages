#
# Maintainer: isiachi <isiachi@rhyeworld.it>
#

pkgname=spl
pkgver=0.6.5.4
_pkgver=0.6.5.4
pkgrel=7
pkgdesc="Solaris Porting Layer kernel modules."
license=('GPL')
makedepends=("git")
depends=("dkms" "spl-utils" "linux-headers")
provides=("spl-dkms")
conflicts=("spl-git" "spl-lts" "spl-dkms")
install=spl.install
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=("spl::git+https://github.com/zfsonlinux/spl.git")
md5sums=('SKIP'
         'a54f0041a9e15b050f25c463f1db7449')
install=spl.install

_kernel=4.3.3

pkgver() {
    cd "${srcdir}/spl"
    printf "%s.r%s_%s" "${_pkgver}" "$(git rev-list --count HEAD)" "${_kernel}"
}

build() {
    cd "${srcdir}/spl"
    ./autogen.sh
    
    _at_enable=""
    [ "${CARCH}" == "i686"  ] && _at_enable="--enable-atomic-spinlocks"

    ./configure --prefix=/usr \
                --libdir=/usr/lib \
                --sbindir=/usr/bin \
                --with-config=user \
                ${_at_enable}

    make
}

package() {
    dkmsdir="${pkgdir}/usr/src/spl-${_pkgver}"
    install -d "${dkmsdir}"

    cd "${srcdir}/spl"
    git archive --format=tar HEAD | tar -x -C "${dkmsdir}"

    cd "${dkmsdir}"
    ./autogen.sh
    scripts/dkms.mkconf -v ${_pkgver} -f dkms.conf -n spl
    chmod g-w,o-w -R .
}

# -*- mode: bash;-*-
