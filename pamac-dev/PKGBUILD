# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Guillaume Benoit <guillaume@manjaro.org>
pkgname=pamac-dev
pkgver=3.2.1
_pkgver=3.2.1
pkgrel=1
_gitcommit="master"
pkgdesc="A Gtk3 frontend for libalpm"
arch=('i686' 'x86_64')
url="https://github.com/manjaro/pamac"
license=('GPL3')
depends=('glib2>=2.46' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'yaourt' 'vte3>=0.38'
		'gtk3>=3.18' 'libnotify' 'desktop-file-utils' 'pacman' 'gnutls>=3.4')
optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome'
			'lxsession: needed for authentification in Xfce, LXDE etc.'
			'yaourt: needed for AUR support')
makedepends=('gettext' 'itstool' 'vala>=0.28' 'git')
backup=('etc/pamac.conf')
provides=('pamac')
conflicts=('pamac' 'pamac-aur')
options=(!emptydirs)
install=pamac.install


if [ "${_gitcommit}" != "" ]; then
	source=("git+https://github.com/manjaro/pamac")
else
	source=("pamac-$pkgver.tar.gz::$url/archive/v$_pkgver.tar.gz")
fi

md5sums=('SKIP')

pkgver() {
	[[ "${srcdir}" != '' ]] && cd "$srcdir/pamac"

	printf "%s.r%s" "$_pkgver" "$(git rev-list --count HEAD)"

}


prepare() {
	{ [[ "${_gitcommit}" != "" ]] &&  cd "${srcdir}/pamac"; } || cd "${srcdir}/pamac-${_pkgver}"

	# adjust version string
	cd src
	sed -i -e "s|\"$_pkgver\"|\"$pkgver-$pkgrel\"|g" manager_window.vala 
	cd ..

	# patches here
	
}

build() {
	{ [[ "${_gitcommit}" != "" ]] &&  cd "${srcdir}/pamac"; } || cd "${srcdir}/pamac-${_pkgver}"

	# build
	make all

}

package() {
	{ [[ "${_gitcommit}" != "" ]] &&  cd "${srcdir}/pamac"; } || cd "${srcdir}/pamac-${_pkgver}"

	make prefix="$pkgdir"/usr sysconfdir="$pkgdir"/etc install

}

# -*- mode: bash;-*-
