# $Id$
# Maintainer: Alexandre Filgueria <alexfilgueira@antergos.com>
# Contributor:  Jan de Groot <jan@archlinux.org>

pkgname=cinnamon-desktop
pkgver=3.0.2
pkgrel=2
pkgdesc="Library with common API for various Cinnamon modules"
arch=(i686 x86_64)
license=(GPL LGPL)
groups=('cinnamon')
depends=('gtk3' 'libxkbfile' 'python2' 'glib2' 'libgsystem' 'libpulse')
makedepends=('intltool' 'gobject-introspection' 'gnome-common' 'python2')
url="https://github.com/linuxmint/cinnamon-desktop"
install=cinnamon-desktop.install
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-desktop/archive/${pkgver}.tar.gz"
        "config.patch")
sha256sums=('b8ea3469806a90765454c204f83fed044917100ebdb83c850371dbae8c34656e'
            'e2c20c35df78aaedcd2d2e4965369bc519f51072ab88d4ac9b01be9a48cf8cfd')
_allowed_in=("staging")

prepare() {
  cd $pkgname-$pkgver
  
  # Arch uses systemd
  patch -Np1 -i ../config.patch

}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
 

