# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-session
pkgver=3.0.1
pkgrel=1
pkgdesc="The Cinnamon Session Handler"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=('dbus-glib' 'cinnamon-desktop' 'libsm' 'libcanberra' 'libsystemd')
makedepends=('intltool' 'mesa' 'json-glib' 'xtrans' 'gnome-common')
options=('!emptydirs')
install=cinnamon-session.install
url="https://github.com/linuxmint/cinnamon-session"
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-session/archive/${pkgver}.tar.gz)
sha256sums=('b738fb4d1f8b93e2217b2c7e0d905e964e0ebde726a4cf5a28296e3eba7daab7')
_allowed_in="staging"

prepare() {
  cd $pkgname-$pkgver

  # Increase timeout, for slow machines
  # patch -Np1 -i ../timeout.patch
  
   # Support new libsystemd (which replaces libsystemd-login)
   sed -i 's/libsystemd-login/libsystemd/' configure.ac

}

build() {
	cd $pkgname-$pkgver
	
	CFLAGS="${CFLAGS} -Wnomaybe-uninitialized" ./autogen.sh \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/cinnamon-session \
		--disable-schemas-compile \
		--enable-systemd \
		--disable-gconf \
		--enable-logind
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
 

