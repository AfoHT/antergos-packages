# $Id: PKGBUILD 126461 2015-01-23 20:22:27Z faidoc $
# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Ner0

pkgname=nemo
pkgver=2.8.7
pkgrel=2
pkgdesc="Cinnamon file manager (Nautilus fork)"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo"
license=('GPL')
depends=('libexif' 'gvfs' 'dconf' 'desktop-file-utils' 'exempi' 'python2'
		'cinnamon-desktop' 'libnotify' 'libxml2' 'cinnamon-translations')
makedepends=('gtk-doc' 'gobject-introspection' 'intltool' 'gnome-common' 'python2-gobject' 'python2-polib')
options=('!emptydirs')
install=nemo.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/nemo/tarball/$pkgver"
		'0001-GTK-3.20-fix-wrong-desktop-size.patch'
		'nemo-gtk-3.20-pathbar-fix.patch'
		'nemo-gtk-3.20-style-classes.patch')
sha256sums=('21f290212bcfb4ac58f7bdc17e9dccfafb59d9fd52a7540a7e199e252a7c2fe4'
            'd85e4daddf916fa51f25ab5d950efda1ab4f266c0f8fe5d015987c306f3ac3d4'
            '410d01a2504d059b10854fbcc77f30a04aa9ed6206cb3ed54d521ea17a102837'
            'b93d549c17ea1e30b52a7ea0a8c8b0262d0b827e96414843bdaaddce01e73bec')
_allowed_in="staging"


prepare() {
	cd linuxmint-nemo-*

	# Python2 fix
	find -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

	# Rename 'Files' app name to avoid having the same as nautilus
	sed -i 's/^Name\(.*\)=.*/Name\1=Nemo/' data/nemo.desktop.in.in
	
	patch -Np1 -i ../0001-GTK-3.20-fix-wrong-desktop-size.patch
	patch -Np1 -i ../nemo-gtk-3.20-pathbar-fix.patch
	patch -Np1 -i ../nemo-gtk-3.20-style-classes.patch
}

build() {
	cd linuxmint-nemo-*

	./autogen.sh --prefix=/usr --sysconfdir=/etc \
		--localstatedir=/var --disable-static \
		--libexecdir=/usr/lib/nemo \
		--disable-update-mimedb \
		--disable-tracker \
		--disable-gtk-doc-html \
		--disable-schemas-compile
	make
}

package() {
	cd linuxmint-nemo-*

	make DESTDIR="$pkgdir/" install
}


