# Maintainer: Antergos Developers <dev@antergos.com>

pkgname='qt5-styleplugins'
_srcname='qtstyleplugins'
pkgver=5.0.0.1r36
pkgrel=1
pkgdesc='Additional style plugins for Qt5'
arch=('i686' 'x86_64')
url="https://github.com/qtproject/${_srcname}"
license=('LGPL')
depends=('qt5-base' 'gconf' 'gtk2' 'libx11')
makedepends=('git')
conflicts=("${pkgname}-git")
_commit='335dbece103e2cbf6c7cf819ab6672c2956b17b3'
source=("${_srcname}-${_commit}.zip::${url}/archive/${_commit}.zip")
md5sums=('fde92bafe18f080497769113d837af5c')


build() {
	cd "${srcdir}/${_srcname}-${_commit}"

	qmake-qt5 PREFIX=/usr LIBDIR=/usr/lib

	make
}

package() {
	cd "${srcdir}/${_srcname}-${_commit}"

	make INSTALL_ROOT="${pkgdir}" install
	rm --recursive "${pkgdir}/usr/lib/cmake"
}
