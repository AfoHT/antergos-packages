# Maintainer: Antergos Devs <devs@antergos.com>

pkgname=numix-icon-theme-square
pkgver=18.08.29
pkgrel=1
epoch=2
pkgdesc="Square icon expansion theme from the Numix project"
arch=('any')
url='http://numixproject.org/'
license=('GPL3')
depends=('numix-icon-theme')
replaces=('numix-icon-theme-square-kde')
conflicts=('numix-icon-theme-square-git')
source=("https://github.com/numixproject/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('04337fc558a4b7718cdc8de80fc93c87aa40178f3cff8a2d20b5af15b60105ce')

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='numixproject'
_monitored_repo='numix-icon-theme-square'
# END ANTBS METADATA

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sed -i 's|Inherits=Numix|Inherits=Numix,Adwaita,gnome,hicolor|g;
			s|Name=Numix Square|Name=Numix-Square|g' Numix-Square/index.theme

	sed -i 's|Inherits=Numix-Light|Inherits=Numix-Light,Numix,Adwaita,gnome,hicolor|g;
			s|Name=Numix Square Light|Name=Numix-Square-Light|g' Numix-Square-Light/index.theme

	install -dm 755 "${pkgdir}/usr/share/icons"
	cp -r Numix-Square{,-Light} "${pkgdir}/usr/share/icons"
}


