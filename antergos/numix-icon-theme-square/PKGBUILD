# Maintainer: Antergos Devs <devs@antergos.com>

pkgname=numix-icon-theme-square
_pkgver=18.08.01
pkgver=18.8.1
pkgrel=1
epoch=1
pkgdesc="Square icon expansion theme from the Numix project"
arch=('any')
url='http://numixproject.org/'
license=('GPL3')
depends=('numix-icon-theme')
replaces=('numix-icon-theme-square-kde')
conflicts=('numix-icon-theme-square-git')
source=("https://github.com/numixproject/${pkgname}/archive/${_pkgver}.tar.gz")
sha256sums=('0b047ffd39a23fef74413a0978dc3070a750d7951185caea8ca8d3042086a6ef')

package() {
	cd "${srcdir}/${pkgname}-${_pkgver}"

	sed -i 's|Inherits=Numix|Inherits=Numix,Adwaita,gnome,hicolor|g;
			s|Name=Numix Square|Name=Numix-Square|g' Numix-Square/index.theme

	sed -i 's|Inherits=Numix-Light|Inherits=Numix-Light,Numix,Adwaita,gnome,hicolor|g;
			s|Name=Numix Square Light|Name=Numix-Square-Light|g' Numix-Square-Light/index.theme

	install -dm 755 "${pkgdir}/usr/share/icons"
	cp -r Numix-Square{,-Light} "${pkgdir}/usr/share/icons"
}


