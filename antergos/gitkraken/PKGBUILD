# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Tim Kleinschmidt <tim.kleinschmidt@gmail.com>
# Contributor: Marcin Wieczorek <marcin@marcin.co>

pkgname=gitkraken
pkgrel=2
pkgver=2.4.0
pkgdesc='The intuitive, fast, and beautiful cross-platform Git client.'
url='http://www.gitkraken.com'
arch=('x86_64')
license=('custom')
groups=('development')
depends=('gtk2' 'git' 'nss' 'libnotify' 'libxtst' 'libgnome-keyring' 'gconf'
		'alsa-lib' 'libcurl-compat')
source=(
	"https://release.gitkraken.com/linux/v${pkgver}.tar.gz"
	'GitKraken.desktop'
	'gitkraken-launcher'
	'gitkraken.png'
	'eula.html'
)
md5sums=('560d36ce2651aacb2f2296fb6771f05c'
         '4b181e5f146d6b1d02b82153549a215f'
         'eb8087b38f93c281f41b5e5d0df918e1'
         '04987b933d551d15c0813111d715bb9e'
         'e9ba6663e5f1f92cae31beb0074e8c6c')

package() {
	install -d "${pkgdir}/opt"
	cp -R "${srcdir}/gitkraken" "${pkgdir}/opt/gitkraken"

	install -d "${pkgdir}"/usr/bin
	install -Dm755 gitkraken-launcher "${pkgdir}/usr/bin/gitkraken"

	install -Dm644 ./eula.html "${pkgdir}/usr/share/licenses/${pkgname}/eula.html"
	install -Dm644 ./GitKraken.desktop "${pkgdir}/usr/share/applications/GitKraken.desktop"
	install -Dm644 ./gitkraken.png "${pkgdir}/usr/share/pixmaps/gitkraken.png"
}

# -*- mode: bash;-*
