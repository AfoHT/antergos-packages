# Maintainer: Antergos Developers <dev@antergos.com>
# Contributors: Achilleas Pipinellis, speed145a, Schnouki, Det

pkgname=firefox-beta
_pkgname=firefox
pkgver=50.0b2
_major=${pkgver/rc*}
_build=${pkgver/*rc}
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org - Beta Channel"
arch=('i686' 'x86_64')
url="https://www.mozilla.org/en-US/firefox/channel#beta"
license=('MPL' 'GPL' 'LGPL')
groups=('browser')
depends=('alsa-lib' 'dbus-glib' 'ffmpeg' 'gtk2' 'gtk3' 'hunspell' 'icu' 'libevent' 'libvpx'
		'libxt' 'mime-types' 'mozilla-common' 'nss' 'startup-notification' 'sqlite' 'ttf-font')
makedepends=('unzip' 'zip' 'diffutils' 'python2' 'yasm' 'mesa' 'imake' 'gconf'
			'libpulse' 'inetutils' 'xorg-server-xvfb' 'autoconf2.13' 'rust')
optdepends=('networkmanager: Location detection via available WiFi networks'
			'upower: Battery API')
provides=("firefox=${pkgver}")
conflicts=('firefox-beta-bin' 'firefox-beta')
options=('!emptydirs' '!makeflags')

if [[ "${_build}" != "${pkgver}" ]]; then
	# Release Candidate
	source=("https://ftp.mozilla.org/pub/firefox/candidates/$_major-candidates/build$_build/source/firefox-$_major.source.tar.xz")
else
	source=("https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/source/firefox-$pkgver.source.tar.xz")
fi

source+=("${pkgname}.desktop"
		'mozconfig'
		'firefox-install-dir.patch'
		'vendor.js'
		'firefox-symbolic.svg')
md5sums=('259ea8127d1f3a72ac23db0a97455472')

# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Antergos Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact info@antergos.com for
# more information.
_google_apps_domain='.apps.googleusercontent.com'
_google_api_key='AIzaSyBPgpho0q0siP2tggJwIsDx8ykpp-Y76Xk'
_google_default_client_id="523794628393-5f327td2rntpgvfvitoe2l36b2k4qdmq${_google_apps_domain}"
_google_default_client_secret='PGTWmwZDJ5VjvFoCKfVc19cK'

# Mozilla API keys (see https://location.services.mozilla.com/api)
# Note: These are for Antergos Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact info@antergos.com for
# more information.
_mozilla_api_key='fa35d4d6-6529-436f-b3a1-8b9af187eca2'


prepare() {
	cd "${_pkgname}-${pkgver}" || cd "${_pkgname}-${_major}"

	cp ../mozconfig .mozconfig
	patch -Np1 -i ../firefox-install-dir.patch

	echo -n "$_google_api_key" >google-api-key
	echo "ac_add_options --with-google-api-keyfile=\"$PWD/google-api-key\"" >>.mozconfig

	echo -n "$_google_default_client_id $_google_default_client_secret" >google-oauth-api-key
	echo "ac_add_options --with-google-oauth-api-keyfile=\"$PWD/google-oauth-api-key\"" >>.mozconfig

	echo -n "$_mozilla_api_key" >mozilla-api-key
	echo "ac_add_options --with-mozilla-api-keyfile=\"$PWD/mozilla-api-key\"" >>.mozconfig

	_python=/usr/bin/python2

	[[ 'i686' = "${CARCH}" ]] && {
		_python="/32build/root${_python}"
		echo 'ac_add_options --target=i686-unknown-linux-gnu' >>.mozconfig
		echo 'export CROSS_COMPILE=1' >> .mozconfig
	}

	mkdir "$srcdir/path"
	
	ln -s ${_python} "$srcdir/path/python"
}


build() {
	cd "${_pkgname}-${pkgver}" || cd "${_pkgname}-${_major}"

	# _FORTIFY_SOURCE causes configure failures
	CPPFLAGS+=" -O2"

	# Hardening
	LDFLAGS+=" -Wl,-z,now"

	# GCC 6
	CXXFLAGS+=" -fno-delete-null-pointer-checks -fno-schedule-insns2"

	export PATH="${srcdir}/path:$PATH"

	# Do PGO
	# xvfb-run -a -s "-extension GLX -screen 0 1280x1024x24" \
	# make -f client.mk build MOZ_PGO=1
	make -f client.mk build
}


package() {
	cd "${_pkgname}-${pkgver}" || cd "${_pkgname}-${_major}"

	make -f client.mk DESTDIR="${pkgdir}" INSTALL_SDK= install

	install -Dm644 ../vendor.js "${pkgdir}/usr/lib/${pkgname}/browser/defaults/preferences/vendor.js"

	for i in 16 22 24 32 48 256; do
		install -Dm644 "browser/branding/official/default${i}.png" \
			"${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/${pkgname}.png"
	done

	_branding='browser/branding/official'
	_hicolor="${pkgdir}/usr/share/icons/hicolor"
	install -Dm644 "${_branding}/content/icon64.png" "${_hicolor}/64x64/apps/${pkgname}.png"
	install -Dm644 "${_branding}/mozicon128.png" "${_hicolor}/128x128/apps/${pkgname}.png"
	install -Dm644 "${_branding}/content/about-logo.png" "${_hicolor}/192x192/apps/${pkgname}.png"
	install -Dm644 "${_branding}/content/about-logo@2x.png" "${_hicolor}/384x384/apps/${pkgname}.png"
	install -Dm644 ../firefox-symbolic.svg "${_hicolor}/symbolic/apps/${pkgname}-symbolic.svg"

	# Desktops
	install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications"

	# Use system-provided dictionaries
	rm -rf "${pkgdir}/usr/lib/${pkgname}"/{dictionaries,hyphenation}
	ln -s /usr/share/hunspell "${pkgdir}/usr/lib/${pkgname}/dictionaries"
	ln -s /usr/share/hyphen "${pkgdir}/usr/lib/${pkgname}/hyphenation"
	
	# Replace duplicate binary with symlink
	# https://bugzilla.mozilla.org/show_bug.cgi?id=658850
	ln -sf firefox "${pkgdir}/usr/lib/${pkgname}/firefox-bin"
}

# -*- mode: bash;-*-
