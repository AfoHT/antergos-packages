# Maintainer: Antergos Developers <dev@antergos.com>

pkgname=lightdm-webkit2-greeter
_pkgname=web-greeter
pkgver=2.2.4
pkgrel=1
pkgdesc='A webkit2 greeter for LightDM'
arch=('i686' 'x86_64')
url='https://github.com/antergos/lightdm-webkit2-greeter'
license=('GPL3')
makedepends=('gobject-introspection' 'meson')
depends=('lightdm' 'webkit2gtk' 'dbus-glib' 'antergos-wallpapers' 'accountsservice')
provides=('lightdm-webkit2-greeter' 'lightdm-webkit-greeter')
conflicts=('lightdm-webkit2-greeter' 'lightdm-webkit-theme-antergos')
replaces=('lightdm-webkit-theme-antergos')
install=greeter.install
backup=("etc/lightdm/${pkgname}.conf")
source=("http://github.com/Antergos/${_pkgname}/archive/${pkgver}.zip")
md5sums=('d4f329f6f02b4842dffeed1869d21d84')
groups=('system')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='antergos'
_monitored_repo='web-greeter'
# END ANTBS METADATA



build() {
	cd "${srcdir}/${_pkgname}-${pkgver}/build"
	
	meson --prefix=/usr --libdir=lib ..
	ninja
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}/build"

	DESTDIR=${pkgdir} ninja install
	cd ..

	sed -i 's|\/usr\/share\/backgrounds|\/usr\/share\/antergos\/wallpapers|g' \
		"${pkgdir}/etc/lightdm/${pkgname}.conf" \
		"${pkgdir}/usr/share/lightdm-webkit/themes/antergos/js/greeter.js"
}



