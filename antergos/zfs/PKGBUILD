# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname=zfs
pkgver=0.7.6
pkgrel=1
pkgdesc="Kernel modules for the Zettabyte File System."
license=('CDDL')
groups=("system" "zfs")
makedepends=("git")
depends=("spl=${pkgver}" "zfs-utils=${pkgver}" "dkms" "linux-headers" "linux")
provides=("zfs-dkms=${pkgver}")
conflicts=("zfs-git" "zfs-dkms")
install=zfs.install
arch=("x86_64")
url="http://zfsonlinux.org/"
_commit=''
_tag="zfs-${pkgver}"
source=("https://github.com/zfsonlinux/${pkgname}/archive/${_tag}.zip")
md5sums=('a139afdab5e37e2a0112811148150c3f')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='zfsonlinux'
_monitored_repo='zfs'
_monitored_match_pattern='/(?<=zfs-|spl-)\S+/'
# END ANTBS METADATA


build() {
    cd "${srcdir}/${pkgname}-${_tag}"
    ./autogen.sh
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --bindir=/usr/bin \
        --sbindir=/usr/bin \
        --libdir=/usr/lib \
        --datadir=/usr/share \
        --includedir=/usr/include \
        --with-udevdir=/usr/lib/udev \
        --libexecdir=/usr/lib/zfs \
        --with-config=user
    make
}

package() {
    dkmsdir="${pkgdir}/usr/src/zfs-${pkgver}"
    install -d "${dkmsdir}"

    cp -a "${srcdir}/${pkgname}-${_tag}"/. ${dkmsdir}

    cd "${dkmsdir}"
    ./autogen.sh
    scripts/dkms.mkconf -v ${pkgver} -f dkms.conf -n ${pkgname}
    chmod g-w,o-w -R .
}
