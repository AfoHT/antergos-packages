# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname=zfs
_pkgver=0.6.5.8
_rev=2019
_kern=4.9.6_1
pkgver="${_pkgver}.r${_rev}.${_kern}"
pkgrel=1
pkgdesc="Kernel modules for the Zettabyte File System."
license=('CDDL')
depends=("spl=${pkgver/2019/964}" "zfs-utils=${pkgver}" "dkms" "linux-headers" "linux<4.10")
provides=("zfs-dkms=${pkgver}")
conflicts=("zfs-git" "zfs-dkms")
install=zfs.install
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
_commit="${_pkgver}"
source=("${pkgname}::https://github.com/zfsonlinux/${pkgname}/archive/zfs-${_pkgver}.zip")
md5sums=('409ef11470958672c3d54fe2249ae484')


# pkgver() {
#	local _kernel='4.5.4_1'
#	cd "${srcdir}/${pkgname}-${_commit}"
#	printf "%s.r%s_%s" "${_pkgver}" "$(git rev-list --count HEAD)" "${_kernel}"
# }

build() {
	cd "${srcdir}/${pkgname}-${pkgname}-${_commit}"
	./autogen.sh
	# scripts/dkms.mkconf -v ${_pkgver} -f dkms.conf -n zfs

	{ [[ "${CARCH}" == i686 ]] && _i686_target="--target=i686-linux-gnu"; } || _i686_target=""

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--libdir=/usr/lib \
		--datadir=/usr/share \
		--includedir=/usr/include \
		--with-udevdir=/usr/lib/udev \
		--libexecdir=/usr/lib/zfs \
		--with-config=user \
		${_i686_target}
	make
}

package() {
	dkmsdir="${pkgdir}/usr/src/zfs-${_pkgver}"
	install -d "${dkmsdir}"

	cp -a "${srcdir}/${pkgname}-${pkgname}-${_commit}"/. ${dkmsdir}

	cd "${dkmsdir}"
	./autogen.sh
	scripts/dkms.mkconf -v ${_pkgver} -f dkms.conf -n zfs
	chmod g-w,o-w -R .
}

# -*- mode: bash;-*-
