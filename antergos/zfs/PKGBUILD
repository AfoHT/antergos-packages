# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname=zfs
_pkgver=0.7.0rc4
_rev=2861
_kern=4.11.2_1
pkgver="${_pkgver}.r${_rev}.${_kern}"
pkgrel=1
pkgdesc="Kernel modules for the Zettabyte File System."
license=('CDDL')
depends=("spl=${pkgver/2861/1026}" "zfs-utils=${pkgver}" "dkms" "linux-headers" "linux<4.12")
provides=("zfs-dkms=${pkgver}")
conflicts=("zfs-git" "zfs-dkms")
install=zfs.install
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
_commit=''
_tag='zfs-0.7.0-rc4'
source=("https://github.com/zfsonlinux/${pkgname}/archive/${_tag}.zip")
md5sums=('0bfe9ad104d446d7e0277abc4e579b83')



build() {
	cd "${srcdir}/${pkgname}-${_tag}"
	./autogen.sh

	{ [[ "${CARCH}" == i686 ]] && _i686_target="--target=i686-linux-gnu"; } || _i686_target=""

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--libdir=/usr/lib \
		--datadir=/usr/share \
		--includedir=/usr/include \
		--with-udevdir=/usr/lib/udev \
		--libexecdir=/usr/lib/zfs \
		--with-config=user \
		${_i686_target}
	make
}

package() {
	dkmsdir="${pkgdir}/usr/src/zfs-${_pkgver}"
	install -d "${dkmsdir}"

	cp -a "${srcdir}/${pkgname}-${_tag}"/. ${dkmsdir}

	cd "${dkmsdir}"
	./autogen.sh
	scripts/dkms.mkconf -v ${_pkgver} -f dkms.conf -n zfs
	chmod g-w,o-w -R .
}


