# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Jason Scurtu (scujas) <jscurtu@gmail.com>
# Contributor: Marcin Tydelski <marcin.tydelski@gmail.com> 
# Contributor: Jan Lukas Gernert (JeanLuc) <https://launchpad.net/~eviltwin1>

pkgname=feedreader
_pkgname=FeedReader
pkgver=1.6.2
pkgrel=1
pkgdesc='A simple feedreader desktop client for web services like Tiny Tiny RSS.'
arch=('i686' 'x86_64')
url='https://github.com/jangernert/FeedReader'
license=('GPL3')
depends=('sqlite3' 'gtk3' 'webkit2gtk' 'libnotify' 'html2text' 'libsoup' 'json-glib' 'libsecret')
makedepends=('vala' 'gobject-introspection' 'cmake')
groups=('rss')
source=("https://github.com/jangernert/FeedReader/archive/v${pkgver}.tar.gz")
md5sums=('1916e6f1f9ce4274b9910634225294e8')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='jangernert'
_monitored_repo='FeedReader'
# END ANTBS METADATA


build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	[[ -d build ]] && rm -rf build

	mkdir build && cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DUSE_WEBKIT_4=ON -DWITH_LIBUNITY=OFF ..
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}/build"
	make DESTDIR="${pkgdir}" install
}

# -*- mode: bash;-*-
