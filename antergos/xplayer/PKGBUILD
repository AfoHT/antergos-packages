# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xplayer
pkgver=1.6.0
pkgrel=1
pkgdesc="Simple cross-desktop media player built with GTK3 by the XApps Project."
arch=('i686' 'x86_64')
license=('GPL2')
groups=('xapps' 'video')
depends=('clutter-gtk' 'clutter-gst' 'xplayer-plparser' 'desktop-file-utils' 'iso-codes'
		'yelp-xsl' 'libpeas' 'gsettings-desktop-schemas' 'python2-gobject' 'gst-plugins-base'
		'gst-plugins-good' 'gst-libav' 'xapps' 'dbus-glib' 'openssl-1.0')
makedepends=('gnome-common' 'gtk-doc' 'gobject-introspection' 'vala')
conflicts=('xplayer-git')
url='https://github.com/linuxmint/xplayer'
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/${pkgname}/archive/${pkgver}.tar.gz")
md5sums=('193d80c1bdb9323275bfe1e8134175b5')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='linuxmint'
_monitored_repo='xplayer'
# END ANTBS METADATA


build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sed -i 's/PYTHON=python/PYTHON=python2/g' py-compile

	./autogen.sh \
		--prefix="/usr" \
		--localstatedir="/var" \
		--libexecdir="/usr/lib/${pkgname}"
	make
}


package(){
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}


