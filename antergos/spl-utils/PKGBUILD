# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname=spl-utils
_pkgver=0.7.10
pkgver=0.7.10git+af567ce
pkgrel=1
license=('CDDL')
pkgdesc="Kernel module support files for the Solaris Porting Layer."
groups=("system" "zfs")
depends=("dkms")
provides=("spl-utils-dkms")
conflicts=("spl-utils-dkms")
arch=("x86_64")
url="http://zfsonlinux.org/"
_commit=''
_tag="spl-${_pkgver%.*}-release"
source=(
	"https://github.com/zfsonlinux/spl/archive/${_tag}.zip"
	'https://github.com/zfsonlinux/spl/pull/707.patch'
)
md5sums=('953595e8d189d3353cf11b0ebf0dd9f2'
         'fa70e1cdbd9f8f0695532f4f1ba3dd88')

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='zfsonlinux'
_monitored_repo='spl'
_monitored_match_pattern='/(?<=zfs-|spl-)\S+/'
# END ANTBS METADATA

prepare() {
    cd "${srcdir}/spl-${_tag}"

    # https://github.com/zfsonlinux/spl/pull/707
    patch -Np1 -i ../707.patch
}

build() {
    cd "${srcdir}/spl-${_tag}"
    ./autogen.sh
    ./configure --prefix=/usr \
        --libdir=/usr/lib \
        --sbindir=/usr/bin \
        --with-config=user
    make
}

package() {
    cd "${srcdir}/spl-${_tag}"
    make DESTDIR="${pkgdir}" install
}
