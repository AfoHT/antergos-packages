# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname=spl-utils
_pkgver=0.7.1
_kern=4128
pkgver="${_pkgver}.${_kern}"
pkgrel=1
license=('CDDL')
pkgdesc="Kernel module support files for the Solaris Porting Layer."
groups=("system" "zfs")
depends=("dkms")
provides=("spl-utils-dkms")
conflicts=("spl-utils-dkms")
arch=("x86_64")
url="http://zfsonlinux.org/"
_commit=''
_tag='spl-0.7.1'
source=("https://github.com/zfsonlinux/spl/archive/${_tag}.zip")
md5sums=('f893c5cad2c04df5712c808afffa780a')

build() {
	cd "${srcdir}/spl-${_tag}"
	./autogen.sh
	./configure --prefix=/usr \
		--libdir=/usr/lib \
		--sbindir=/usr/bin \
		--with-config=user
	make
}

package() {
	cd "${srcdir}/spl-${_tag}"
	make DESTDIR="${pkgdir}" install
}


