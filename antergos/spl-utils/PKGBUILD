# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname=spl-utils
_pkgver=0.6.5.10
_rev='+git0'
_kern=4.11.7_1
pkgver="${_pkgver}${_rev}.${_kern}"
pkgrel=1
epoch=1
license=('CDDL')
pkgdesc="Kernel module support files for the Solaris Porting Layer."
groups=("system" "zfs")
depends=("dkms")
provides=("spl-utils-dkms=${pkgver}")
conflicts=("spl-utils-dkms")
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
_commit=''
_tag='spl-0.6.5.10'
source=("https://github.com/zfsonlinux/spl/archive/${_tag}.zip")
md5sums=('ad8b084453fa6678578ec4a9773ef3bf')



build() {
	cd "${srcdir}/spl-${_tag}"
	./autogen.sh

	{ [[ "${CARCH}" == i686 ]] && _at_enable="--enable-atomic-spinlocks"; } || _at_enable=""
	{ [[ "${CARCH}" == i686 ]] && _i686_target="--target=i686-linux-gnu"; } || _i686_target=""

	./configure --prefix=/usr \
		--libdir=/usr/lib \
		--sbindir=/usr/bin \
		--with-config=user \
		${_at_enable} \
		${_i686_target}

	make
}

package() {
	cd "${srcdir}/spl-${_tag}"
	make DESTDIR="${pkgdir}" install
}


