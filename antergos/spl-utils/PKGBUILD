# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname='spl-utils'
_pkgver=0.7.0rc4
_rev=1
_kern=4.11.2_1
pkgver="${_pkgver}.r${_rev}.${_kern}"
pkgrel=1
license=('CDDL')
pkgdesc="Kernel module support files for the Solaris Porting Layer."
groups=("system" "zfs")
depends=("dkms")
provides=("spl-utils-dkms=${pkgver}")
conflicts=("spl-utils-dkms")
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
_commit='3665037f301b83144d198676f06a782f3273676f'
source=("https://github.com/zfsonlinux/spl/archive/${_commit}.zip")
md5sums=('d9f44fa6a99a970c20fd83f3943051d8')



build() {
	cd "${srcdir}/spl-${_commit}"
	./autogen.sh

	{ [[ "${CARCH}" == i686 ]] && _at_enable="--enable-atomic-spinlocks"; } || _at_enable=""
	{ [[ "${CARCH}" == i686 ]] && _i686_target="--target=i686-linux-gnu"; } || _i686_target=""

	./configure --prefix=/usr \
		--libdir=/usr/lib \
		--sbindir=/usr/bin \
		--with-config=user \
		${_at_enable} \
		${_i686_target}

	make
}

package() {
	cd "${srcdir}/spl-${_commit}"
	make DESTDIR="${pkgdir}" install
}


