# Maintainer: Antergos Developers <dev@antergos.com>

pkgname=antergos-repo-priority
pkgver=1.0.6
pkgrel=2
pkgdesc="Automatically adjusts the priority of the antergos repo in pacman.conf as needed."
arch=('any')
depends=('pacman' 'python-termcolor' 'libnotify' 'gettext')
groups=('antergos-base')
url="https://antergos.com/"
license=('GPL')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.zip::https://github.com/Antergos/${pkgname}/archive/${pkgver}.zip")
md5sums=('5f2a9e5e7cc4c9d9b86668dd32316653')

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='antergos'
_monitored_repo='antergos-repo-priority'
# END ANTBS METADATA

package() {
	install -d ${pkgdir}/usr/share/{pixmaps,locale}
	install -d "${pkgdir}/usr/share/libalpm/hooks"
	install -d "${pkgdir}/usr/bin"

	cd "${srcdir}/${pkgname}-${pkgver}"
	mv 'logo-square32.png' 'antergos-logo-square32.png'

	install -m777 -t "${pkgdir}/usr/share/libalpm/hooks/" "99-${pkgname}.hook"
	install -m777 -t "${pkgdir}/usr/bin/" "${pkgname}.py"
	install -m777 -t "${pkgdir}/usr/bin" 'antergos-notify.sh'
	install -m644 -t "${pkgdir}/usr/share/pixmaps" 'antergos-logo-square32.png'

	for fpath in po/*; do
		if [[ -f "${fpath}" ]] && [[ "${fpath}" != 'po/ANTERGOS_NOTIFY.pot' ]]; then
			STRING_PO=`echo ${fpath#*/}`
			STRING=`echo ${STRING_PO%.po}`
			mkdir -p "${pkgdir}/usr/share/locale/${STRING}/LC_MESSAGES"
			msgfmt "${fpath}" -o "${pkgdir}/usr/share/locale/${STRING}/LC_MESSAGES/ANTERGOS_NOTIFY.mo"
			echo "${STRING} installed..."
		fi
	done
}


