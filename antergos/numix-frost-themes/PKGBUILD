# Maintainer: Antergos Devs <dev@antergos.com>

pkgname=numix-frost-themes
_pkgname=Numix-Frost
_light=Numix-Frost-Light
_branch=numix-frost-light
pkgver=3.6.5
pkgrel=1.1
pkgdesc='Official Numix GTK theme - Antergos Edition'
arch=('any')
license=('GPL3')
groups=('themes' 'themes::gtk')
url='http://numixproject.org/'
depends=('gtk-engine-murrine' 'gtk3>=3.18')
makedepends=('ruby-sass' 'npm' 'gulp')
replaces=('gtk-theme-numix-frost')
#source=('git+https://github.com/Antergos/Numix-Frost.git')
source=("${pkgname}-${pkgver}::https://github.com/Antergos/Numix-Frost/archive/${pkgver}.zip"
		'fix-headerbar-regression.patch::https://github.com/Antergos/Numix-Frost/commit/c21a63a110206829511b25bfac8f7f42e91f93cc.patch')
md5sums=('1e02fefbcb55089342e3395eea0d8402'
		'0e5198a274e9c5f8e2bbff31afa3e566')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='antergos'
_monitored_repo='numix-frost'
# END ANTBS METADATA


build() {
	git clone https://github.com/Antergos/Numix-Frost.git "${srcdir}/${_pkgname}"
	git clone https://github.com/Antergos/Numix-Frost.git "${srcdir}/${_light}"
}


package() {

	# Numix-Frost
	cd "${srcdir}/${_pkgname}"
	git checkout "${pkgver}"
	patch -Np1 -i ../fix-headerbar-regression.patch
	git submodule update --init -- src/includes/cinnamon
	sed -i 's| unity||g' scripts/utils.sh
	make
	make DESTDIR="${pkgdir}" install


	# Cinnamon Theme
	sed -i 's%Numix-Cinnamon%Numix-Frost%g' src/includes/cinnamon/Numix-Cinnamon/cinnamon/theme.json
	install -d "${pkgdir}/usr/share/themes/${_pkgname}/cinnamon"
	cd src/includes/cinnamon/Numix-Cinnamon
	scss --update --sourcemap=none cinnamon/sass:cinnamon
	rm -rf cinnamon/sass
	rm -f cinnamon/*.js
	cp -R --no-preserve=ownership cinnamon "$pkgdir/usr/share/themes/$_pkgname"


	# GNOME Shell Theme
	install -d "${pkgdir}/usr/share/themes/${_pkgname}/gnome-shell"
	cd "${srcdir}/${_pkgname}/src/gnome-shell"
	find . -type f -exec install -Dm644 '{}' "$pkgdir/usr/share/themes/$_pkgname/gnome-shell/{}" \;


	# Numix-Frost-Light
	cd "${srcdir}/${_light}"
	git checkout "${pkgver}-light"
	patch -Np1 -i ../fix-headerbar-regression.patch
	sed -i 's| unity||g' scripts/utils.sh
	make
	make DESTDIR="${pkgdir}" install

}

# -*- mode: bash;-*-
