# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Armin K. <krejzi at email dot com>
# Contributor: Austin ( doorknob60 [at] gmail [dot] com )
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=broadcom-wl
_pkgver=6.30.223.271
pkgver=6.30.223.271
pkgrel=1
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
url='http://www.broadcom.com/support/802.11/linux_sta.php'
arch=('i686' 'x86_64')
license=('custom')
depends=('linux' 'dkms' 'linux-headers' 'crda')
optdepends=('linux-lts-headers: build modules against Arch LTS kernel')
provides=('broadcom-wl-dkms')
conflicts=('broadcom-wl-dkms')
backup=('etc/modprobe.d/broadcom-wl-dkms.conf')
source=('broadcom-wl-dkms.conf'
		'license.patch'
		'wl_linux.c.patch'
		'dkms.conf.in'
		'linux47.patch'
		'linux48.patch'
		'linux411.patch')
_base_url='https://docs.broadcom.com/docs-and-downloads/docs/linux_sta'
source_i686=("${_base_url}/hybrid-v35-nodebug-pcoem-${pkgver//./_}.tar.gz")
source_x86_64=("${_base_url}/hybrid-v35_64-nodebug-pcoem-${pkgver//./_}.tar.gz")

md5sums=('72664135416b0284c4fedd57374ecfcc'
         '1646825ba423f9c896e242313a411238'
         'fabbf528164f5e3a9dcf46f46a678f86'
         'b5b6ec0039e6acd379cca964f561210b'
         'e1bec4e95a26bf186db702137d7a5748'
         '44ffbb144d8440d7ee8452ab1b0fcf57'
         '15a0523447fc131002fa75a3063893a8')
md5sums_i686=('4e75f4cb7d87f690f9659ffc478495f0')
md5sums_x86_64=('115903050c41d466161784d4c843f4f9')

_kernver="$(pacman -Sl core | grep ' linux ' | cut -d ' ' -f 3)"


pkgver() {
	echo "${_pkgver}_${_kernver/-/_}"
}

prepare() {
	patch -p1 -i license.patch
	patch -p1 -i wl_linux.c.patch
	patch -p1 -i linux47.patch
	patch -p1 -i linux48.patch
	patch -p1 -i linux411.patch

	sed -e "/BRCM_WLAN_IFNAME/s:eth:wlan:" -i src/wl/sys/wl_linux.c

	sed -e "s/@PACKAGE_VERSION@/${_pkgver}/" dkms.conf.in > dkms.conf
}

package() {

	local dest="${pkgdir}/usr/src/${pkgname}-${pkgver}"

	mkdir -p "${dest}"
	cp -RL src lib Makefile dkms.conf "${dest}"
	chmod a-x "${dest}/lib/LICENSE.txt" # Ships with executable bits set

	mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
	ln -rs "${dest}/lib/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	
	install -Dm 644 broadcom-wl-dkms.conf "${pkgdir}/etc/modprobe.d/broadcom-wl-dkms.conf"
}


