# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-session-manager
pkgver=1.16.0
pkgrel=1
pkgdesc="The MATE Session Handler"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('dbus-glib' 'gtk3' 'libsm' 'libxtst' 'mate-desktop>=1.14'
		 'mate-polkit>=1.14' 'mate-settings-daemon>=1.14' 'ttf-dejavu'
		 'xdg-user-dirs')
makedepends=('mate-common' 'xmlto' 'xtrans')
optdepends=('onboard: On-screen keyboard useful for mobility impaired users'
			'orca: Screen reader for blind or visually impaired individuals'
			'xdg-user-dirs-gtk: Add autostart that prompts to rename user directories when switching locales')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('553b28bbbd9164e0403783db58015fa42d3ae38d')

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='mate-desktop'
_monitored_match_pattern='1.16'
# END ANTBS METADATA

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/${pkgname} \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-gtk=3.0 \
		--disable-upower
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
 
 

# -*- mode: bash;-*-
 
