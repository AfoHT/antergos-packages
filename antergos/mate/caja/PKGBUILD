# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=caja
pkgver=1.18.0
pkgrel=1
pkgdesc='File manager for the MATE desktop'
url='http://mate-desktop.org'
arch=('i686' 'x86_64')
license=('GPL')
depends=('exempi' 'gtk3' 'gvfs' 'libexif' 'python-gobject' 'libxt' 'mate-desktop' 'startup-notification')
makedepends=('wget' 'gobject-introspection' 'mate-common')
optdepends=('gstreamer: automatic media playback when mouseover'
			'gvfs-smb: To connect to Samba/Windows shares')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('7d4088e151435ccfea442f03b796a9e0f3f56a69')

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='mate-desktop'
_monitored_match_pattern='/^\d\.\d[02468]\.\d+$/'
# END ANTBS METADATA

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/${pkgname} \
		--enable-introspection \
		--disable-static \
		--disable-update-mimedb
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}

# -*- mode: bash;-*-
