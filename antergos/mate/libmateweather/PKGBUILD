# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=libmateweather
pkgver=1.16.1
pkgrel=1
pkgdesc="Provides access to weather information from the Internet."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('LGPL')
groups=('mate-extra')
depends=('dconf' 'gtk3' 'libsoup')
makedepends=('mate-common')
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('912eb67104da9414eb47c4a9b0631735d2528794')

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='mate-desktop'
_monitored_match_pattern='1.16'
# END ANTBS METADATA

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-gtk=3.0 \
		--disable-static \
		--disable-python \
		--enable-locations-compression
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
 
 

# -*- mode: bash;-*-
 
