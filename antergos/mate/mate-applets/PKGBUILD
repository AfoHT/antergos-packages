# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-applets
pkgver=1.16.0
pkgrel=1
pkgdesc="Applets for MATE panel"
arch=('i686' 'x86_64')
url="http://mate-desktop.org"
license=('GPL')
depends=('gtksourceview3' 'libgtop' 'libnotify' 'mate-panel>=1.14'
		 'polkit' 'python2-dbus' 'python2-gobject' 'upower')
makedepends=('docbook2x' 'docbook-xsl' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
replaces=('mate-netspeed' 'mate-netspeed-gtk3')
conflicts=('mate-netspeed' 'mate-netspeed-gtk3')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('6e6b51089bcb6450a466ba358b71fcfd7aba3a6a')

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='mate-desktop'
_monitored_match_pattern='1.16'
# END ANTBS METADATA

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	grep -RZl 'env python' | xargs -0 -l sed -i 's/env python/env python2/'
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	PYTHON=/usr/bin/python2 ./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib/${pkgname} \
		--enable-polkit \
		--enable-ipv6 \
		--with-gtk=3.0 \
		--disable-static
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
 

# -*- mode: bash;-*-
 
