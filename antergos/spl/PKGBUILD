# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname=spl
_pkgver=0.6.5.10
_rev='+git0'
_kern=4.11.7_1
pkgver="${_pkgver}${_rev}.${_kern}"
pkgrel=2
epoch=1
pkgdesc="Solaris Porting Layer kernel modules."
license=('GPL')
makedepends=("git")
depends=("dkms" "spl-utils=1:${pkgver}" "linux-headers")
provides=("spl-dkms=${pkgver}")
conflicts=("spl-dkms")
install=spl.install
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
_commit=''
_tag='spl-0.6.5.10'
source=(
	"https://github.com/zfsonlinux/spl/archive/${_tag}.zip"
	'spl-dont-take-spin-lock-on-rwlock-owner.patch::https://github.com/zfsonlinux/spl/commit/4f8e643afe9d8cc558566e1dda05ef597cb2526e.patch'
)
md5sums=('ad8b084453fa6678578ec4a9773ef3bf'
         '0b63bc0830502045ef3078606e59e6f1')
install=spl.install


prepare() {
	cd "${srcdir}/${pkgname}-${_tag}"
	patch -Np1 -i ../spl-dont-take-spin-lock-on-rwlock-owner.patch
}

build() {
	cd "${srcdir}/${pkgname}-${_tag}"
	./autogen.sh

	{ [[ "${CARCH}" == i686 ]] && _at_enable="--enable-atomic-spinlocks"; } || _at_enable=""
	{ [[ "${CARCH}" == i686 ]] && _i686_target="--target=i686-linux-gnu"; } || _i686_target=""

	./configure \
		--prefix=/usr \
		--libdir=/usr/lib \
		--sbindir=/usr/bin \
		--with-config=user \
		${_at_enable} \
		${_i686_target}

	make
}

package() {
	dkmsdir="${pkgdir}/usr/src/spl-${_pkgver}"
	install -d "${dkmsdir}"

	cp -a "${srcdir}/${pkgname}-${_tag}"/. ${dkmsdir}

	cd "${dkmsdir}"
	./autogen.sh
	scripts/dkms.mkconf -v ${_pkgver} -f dkms.conf -n spl
	chmod g-w,o-w -R .
}


