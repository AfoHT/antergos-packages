# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname=spl
pkgver=0.7.2
pkgrel=1
pkgdesc="Solaris Porting Layer kernel modules."
license=('GPL')
makedepends=("git")
depends=("spl-utils=${pkgver}" "dkms" "linux-headers" "linux")
provides=("spl-dkms=${pkgver}")
conflicts=("spl-dkms")
install=spl.install
arch=("x86_64")
url="http://zfsonlinux.org/"
_commit=''
_tag='spl-0.7.2'
source=("https://github.com/zfsonlinux/${pkgname}/archive/${_tag}.zip")
md5sums=('acecdc2b3b1e064082b718fc9251c2d5')

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='zfsonlinux'
_monitored_repo='spl'
_monitored_match_pattern='/(?<=zfs-|spl-)\S+/'
# END ANTBS METADATA


build() {
    cd "${srcdir}/${pkgname}-${_tag}"
    ./autogen.sh
    ./configure \
        --prefix=/usr \
        --libdir=/usr/lib \
        --sbindir=/usr/bin \
        --with-config=user
    make
}

package() {
	dkmsdir="${pkgdir}/usr/src/spl-${pkgver}"
	install -d "${dkmsdir}"

	cp -a "${srcdir}/${pkgname}-${_tag}"/. ${dkmsdir}

	cd "${dkmsdir}"
	./autogen.sh
	scripts/dkms.mkconf -v ${pkgver} -f dkms.conf -n ${pkgname}
	chmod g-w,o-w -R .
}
