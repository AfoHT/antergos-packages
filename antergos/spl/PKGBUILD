# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname=spl
pkgver=0.7.3
pkgrel=2
pkgdesc="Solaris Porting Layer kernel modules."
license=('GPL')
makedepends=("git")
depends=("spl-utils=${pkgver}" "dkms" "linux-headers" "linux")
provides=("spl-dkms=${pkgver}")
conflicts=("spl-dkms")
install=spl.install
arch=("x86_64")
url="http://zfsonlinux.org/"
_commit=''
_tag='spl-0.7.3'
source=(
	"https://github.com/zfsonlinux/${pkgname}/archive/${_tag}.zip"
	'linux-4.14.patch::https://github.com/zfsonlinux/spl/commit/ed19bccfb651843fa208232b3a2d3d22a4152bc8.patch'
)
md5sums=('9d95353a3410b0bd6a7ecdc21fe770a4'
         'd3e6375f67bcd1be2d04d18dbe5f8fc9')

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='zfsonlinux'
_monitored_repo='spl'
_monitored_match_pattern='/(?<=zfs-|spl-)\S+/'
# END ANTBS METADATA

prepare() {
	# Linux 4.14
	patch -Np1 -i ../linux-4.14.patch
}

build() {
    cd "${srcdir}/${pkgname}-${_tag}"
    ./autogen.sh
    ./configure \
        --prefix=/usr \
        --libdir=/usr/lib \
        --sbindir=/usr/bin \
        --with-config=user
    make
}

package() {
	dkmsdir="${pkgdir}/usr/src/spl-${pkgver}"
	install -d "${dkmsdir}"

	cp -a "${srcdir}/${pkgname}-${_tag}"/. ${dkmsdir}

	cd "${dkmsdir}"
	./autogen.sh
	scripts/dkms.mkconf -v ${pkgver} -f dkms.conf -n ${pkgname}
	chmod g-w,o-w -R .
}
