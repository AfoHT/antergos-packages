# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname=spl
_pkgver=0.7.0
_rev=1038
_kern=4.12.3_1
pkgver="${_pkgver}.r${_rev}.${_kern}"
pkgrel=1
pkgdesc="Solaris Porting Layer kernel modules."
license=('GPL')
makedepends=("git")
depends=("dkms" "spl-utils=${pkgver}" "linux-headers")
provides=("spl-dkms=${pkgver}")
conflicts=("spl-dkms")
install=spl.install
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
_commit=''
_tag='spl-0.7.0'
source=("https://github.com/zfsonlinux/spl/archive/${_tag}.zip")
md5sums=('1b115bcf162373a300729ac718f73468')
install=spl.install


build() {
	cd "${srcdir}/${pkgname}-${_tag}"
	./autogen.sh

	{ [[ "${CARCH}" == i686 ]] && _at_enable="--enable-atomic-spinlocks"; } || _at_enable=""
	{ [[ "${CARCH}" == i686 ]] && _i686_target="--target=i686-linux-gnu"; } || _i686_target=""

	./configure \
		--prefix=/usr \
		--libdir=/usr/lib \
		--sbindir=/usr/bin \
		--with-config=user \
		${_at_enable} \
		${_i686_target}

	make
}

package() {
	dkmsdir="${pkgdir}/usr/src/spl-${_pkgver}"
	install -d "${dkmsdir}"

	cp -a "${srcdir}/${pkgname}-${_tag}"/. ${dkmsdir}

	cd "${dkmsdir}"
	./autogen.sh
	scripts/dkms.mkconf -v ${_pkgver} -f dkms.conf -n spl
	chmod g-w,o-w -R .
}


