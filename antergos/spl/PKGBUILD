# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname=spl
_pkgver=0.7.10
pkgver=0.7.10git+af567ce
pkgrel=1
pkgdesc="Solaris Porting Layer kernel modules."
groups=("system" "zfs")
license=('GPL')
makedepends=("git")
depends=("spl-utils>=${pkgver%+*}" "dkms" "linux-headers" "linux>4.18")
provides=("spl-dkms=${pkgver}")
conflicts=("spl-dkms")
install=spl.install
arch=("x86_64")
url="http://zfsonlinux.org/"
_commit=''
_tag="spl-${_pkgver%.*}-release"
source=(
	"https://github.com/zfsonlinux/${pkgname}/archive/${_tag}.zip"
	'https://github.com/zfsonlinux/spl/pull/707.patch'
)
md5sums=('953595e8d189d3353cf11b0ebf0dd9f2'
         'fa70e1cdbd9f8f0695532f4f1ba3dd88')

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='zfsonlinux'
_monitored_repo='spl'
_monitored_match_pattern='/(?<=zfs-|spl-)\S+/'
# END ANTBS METADATA

prepare() {
    cd "${srcdir}/${pkgname}-${_tag}"
    
    # https://github.com/zfsonlinux/spl/pull/707
    patch -Np1 -i ../707.patch
}

build() {
    cd "${srcdir}/${pkgname}-${_tag}"
    ./autogen.sh
    ./configure \
        --prefix=/usr \
        --libdir=/usr/lib \
        --sbindir=/usr/bin \
        --with-config=user
    make
}

package() {
    dkmsdir="${pkgdir}/usr/src/spl-${_pkgver}"
    install -d "${dkmsdir}"

    cp -a "${srcdir}/${pkgname}-${_tag}"/. ${dkmsdir}

    cd "${dkmsdir}"
    ./autogen.sh
    scripts/dkms.mkconf -v ${_pkgver} -f dkms.conf -n ${pkgname}
    chmod g-w,o-w -R .
}
