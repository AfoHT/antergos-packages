# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: M0Rf30
# Contributor: unifiedlinux
# Contributor: CReimer

pkgname=cinnamon
pkgver=3.2.1
pkgrel=1
pkgdesc='Linux Desktop featuring a traditional layout built using modern technologies.'
arch=('i686' 'x86_64')
url='http://cinnamon.linuxmint.com/'
license=('GPL2')
groups=('cinnamon')

depends=('accountsservice'          'cjs'                   'librsvg'                'python2-lxml'
		 'caribou'                  'clutter-gtk'           'muffin'                 'python2-pam'
		 'cinnamon-control-center'  'gnome-backgrounds'     'nemo'                   'python2-pexpect'
		 'cinnamon-menus'           'gnome-themes-standard' 'networkmanager'         'python2-pillow'
		 'cinnamon-screensaver'     'gconf'                 'network-manager-applet' 'python2-ptyprocess'
		 'cinnamon-session'         'gstreamer'             'polkit-gnome'           'python2-pyinotify'
		 'cinnamon-settings-daemon' 'libgnome-keyring'      'python2-cairo'          'webkitgtk'
		 'cinnamon-translations'    'libgnomekbd'           'python2-dbus')

makedepends=('autoconf-archive' 'intltool' 'gtk-doc' 'gobject-introspection')
options=('!emptydirs')
_themes_commit='0da899f58c435153cb12bec9a41e7f5ad30b6633'
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/Cinnamon/archive/${pkgver}.tar.gz"
		"themes.tar.gz::https://github.com/linuxmint/cinnamon-themes/archive/${_themes_commit}.tar.gz"
		'set_wheel.patch'
		'default-theme.patch')
md5sums=('a954caa4e4d817bed7e461a43750c47e'
         '3888cddad5faff248fffdd17fcf26d41'
         '68adf4662e44e23c9165f2d1573b818b'
         '089b7ec2aa9e2617e6fac026e9f04de8')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='linuxmint'
_monitored_repo='cinnamon'
# END ANTBS METADATA


prepare() {
	cd "${srcdir}"/Cinnamon*

	# Use wheel group instread of sudo
	patch -Np1 -i ../set_wheel.patch

	# Set default theme to 'cinnamon'
	patch -Np1 -i ../default-theme.patch

	# Python2 fix
	find -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

	# Add polkit agent to required components
	sed -i 's/RequiredComponents=\(.*\)$/RequiredComponents=\1polkit-gnome-authentication-agent-1;/' \
		files/usr/share/cinnamon-session/sessions/cinnamon*.session

	# Use pkexec instead of gksu
	sed -i 's/gksu/pkexec/' files/usr/bin/cinnamon-settings-users

	# Fix selected background color in Cinnamon Settings for Adwaita theme
	sed -i 's/@selected_bg_color;/@theme_selected_bg_color;/' \
	files/usr/share/cinnamon/cinnamon-settings/cinnamon-settings.py
}

build() {
	cd "${srcdir}"/Cinnamon*

	./autogen.sh \
		--prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib/cinnamon \
		--localstatedir=/var \
		--disable-static \
		--disable-schemas-compile \
		--enable-compile-warnings=yes

	make
}

package() {
	cd "${srcdir}"/Cinnamon*
	make DESTDIR="${pkgdir}" install

	mkdir -p "${pkgdir}/usr/share/themes"
	cd "${srcdir}"/cinnamon-themes*
	cp -R usr/share/themes/* "${pkgdir}/usr/share/themes"
}

# -*- mode: bash;-*-
