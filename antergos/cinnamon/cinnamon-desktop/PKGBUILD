# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor:  Jan de Groot <jan@archlinux.org>

pkgname=cinnamon-desktop
pkgver=3.2.2
pkgrel=2
pkgdesc="Library with common API for various Cinnamon modules"
url="https://github.com/linuxmint/cinnamon-desktop"
arch=(i686 x86_64)
license=(GPL LGPL)
groups=('cinnamon')
depends=('gtk3' 'libxkbfile' 'python2' 'glib2' 'libgsystem' 'libpulse'
		'gobject-introspection' 'python2-gobject')
makedepends=('intltool' 'gnome-common')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-desktop/archive/${pkgver}.tar.gz"
		'config.patch'
		'https://github.com/linuxmint/cinnamon-desktop/pull/82.patch')
md5sums=('1aaf67d3ae6e94236066805192778817'
		'4e82d86d4c008e89c7681279146e4292'
		'6369736d40adf9487ae7a7db8f8207f5')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='linuxmint'
_monitored_repo='cinnamon-desktop'
# END ANTBS METADATA


prepare() {
	cd "${pkgname}-${pkgver}"

	# Arch uses systemd
	patch -Np1 -i ../config.patch

	# https://github.com/linuxmint/cinnamon-desktop/pull/82
	patch -Np1 -i ../82.patch
}

build() {
	cd "${pkgname}-${pkgver}"

	./autogen.sh \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/"${pkgname}" \
		--disable-static

	make
}

package() {
	cd "${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install
}
 
# -*- mode: bash;-*-
