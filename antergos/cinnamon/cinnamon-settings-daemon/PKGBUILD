# $Id: PKGBUILD 133859 2015-05-20 01:34:39Z faidoc $
# Maintainer: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=3.2.1
pkgrel=1
pkgdesc="The Cinnamon Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
groups=('cinnamon')
depends=('cinnamon-desktop' 'libibus' 'libcanberra-pulse' 'librsvg' 'nss' 'polkit' 'lcms2'
		'pulseaudio-alsa' 'upower' 'libnotify' 'libgnomekbd' 'libwacom' 'dbus-glib' 'colord')
makedepends=('intltool' 'docbook-xsl' 'autoconf-archive' 'xf86-input-wacom' 'cups')
options=('!emptydirs')
url="https://github.com/linuxmint/cinnamon-settings-daemon"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-settings-daemon/archive/${pkgver}.tar.gz"
		'http://pkgs.fedoraproject.org/cgit/rpms/cinnamon-settings-daemon.git/plain/csd-background-manager.patch')
md5sums=('c2d20f81d266a243e325298a45fd0510'
         '569d2e9848186353897550ea0c7bcc45')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='linuxmint'
_monitored_repo='cinnamon-settings-daemon'
# END ANTBS METADATA


build() {
	cd "${pkgname}-${pkgver}"

	# https://github.com/linuxmint/Cinnamon/issues/5569#issuecomment-252537458
	patch -Np1 -i ../csd-background-manager.patch

	./autogen.sh \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir="/usr/lib/${pkgname}" \
		--disable-static \
		--enable-systemd \
		--enable-polkit

	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
 

 
