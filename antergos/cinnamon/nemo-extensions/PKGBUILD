# Maintainer: Antergos Developers <dev@antergos.com>

pkgbase=nemo-extensions
pkgname=(
	'nemo-audio-tab'
	'nemo-emblems'
	'nemo-fileroller'
	'nemo-preview'
	'nemo-python'
	'nemo-share'
	'nemo-seahorse'
	'nemo-dropbox'
)
pkgver=3.4.0
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL2')
url="https://github.com/linuxmint/nemo-extensions"
groups=('cinnamon')
depends=('nemo' 'file-roller' 'cjs' 'clutter-gtk' 'clutter-gst' 'gst-plugins-good' 'libmusicbrainz5'
		'evince' 'gtksourceview3' 'webkit2gtk' 'python2-gobject' 'libcryptui' 'gcr' 'samba'
		'libgnome-keyring' 'python2-mutagen')
makedepends=('gnome-common' 'gtk-doc' 'intltool' 'gobject-introspection' 'python2-distutils-extra'
			'python2-docutils' 'pygtk' 'python2-gobject2')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/linuxmint/nemo-extensions/archive/${pkgver}.tar.gz")
sha256sums=('68dadce98c17321c3c22e596452a1b7aa0842c5faa05688734430857e037b3f2')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='linuxmint'
_monitored_repo='nemo-extensions'
# END ANTBS METADATA


_make_install() {
	cd "${pkgbase}-${pkgver}/${pkgname}"
	make DESTDIR="${pkgdir}" install
}

_setup.py() {
	cd "${pkgbase}-${pkgver}/${pkgname}"
	python2 ./setup.py install --root="${pkgdir}" --prefix=/usr --optimize=1
	cd -
}

prepare() {
	cd ${pkgbase}-${pkgver}

	# Python2 fix
	find -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
	sed -i 's/<<EOF | python/&2/g' nemo-dropbox/configure.ac
	sed -i 's|python|&2|g' nemo-dropbox/Makefile.am

	# Fix error in nemo-dropbox Makefile
	sed -i 's|dropbox.txt.in dropbox|dropbox dropbox.txt.in|g' nemo-dropbox/Makefile.am

	# Fix path for nemo-python
	sed -i 's|libdirsuffix="/i386-linux-gnu/"|libdirsuffix=""|' nemo-python/m4/python.m4

	# Fix conflict with nautilus-seahorse
	cd nemo-seahorse
	sed -i '
		s|\("seahorse-tool"\)|("seahorse-tool-nemo")|g;
		s| seahorse-tool | seahorse-tool-nemo |g;
		s|org.gnome.seahorse.nautilus|org.nemo.seahorse|g' \
		nemo-ext/nemo-seahorse.c \
		tool/seahorse-tool.1 \
		data/org.gnome.seahorse.nautilus.convert \
		data/org.gnome.seahorse.nautilus.gschema.xml \
		data/org.gnome.seahorse.nautilus.window.gschema.xml
}

build() {
	cd ${pkgbase}-${pkgver}

	_configure_pkgs=(
		'nemo-dropbox'
		'nemo-fileroller'
		'nemo-seahorse'
		'nemo-share'
	)

	_autogen_pkgs=(
		'nemo-preview'
		'nemo-python'
	)

	_distutils_pkgs=(
		'nemo-audio-tab'
		'nemo-emblems'
	)

	for pkg in "${_configure_pkgs[@]}"
	do
		cd "${pkg}"
		
		if [[ "${pkg}" = 'nemo-seahorse' ]]; then
			_extra_args='--program-suffix=-nemo'
		else
			_extra_args=''
		fi

 		PYTHON=python2 autoreconf -fi

		if [[ "${pkg}" = 'nemo-dropbox' ]]; then
			sed -i 's|python rst2man.py|python2 /usr/bin/rst2man2.py|g' configure
		fi

		PYTHON=python2 ./configure \
			--prefix=/usr \
			--sysconfdir=/etc \
			--localstatedir=/var \
			--libexecdir="/usr/lib/${pkg}" \
			--disable-static \
			--disable-schemas-compile \
			"${_extra_args}"
		make
		cd -
	done


	for pkg in "${_autogen_pkgs[@]}"
	do
		cd "${pkg}"
		PYTHON=/usr/bin/python2 ./autogen.sh \
			--prefix=/usr \
			--sysconfdir=/etc \
			--localstatedir=/var \
			--libexecdir="/usr/lib/${pkg}" \
			--disable-static \
			--disable-schemas-compile
		make
		cd -
	done

	for pkg in "${_distutils_pkgs[@]}"
	do
		cd "${pkg}"
		python2 ./setup.py build
		cd -
	done
}

package_nemo-audio-tab() {
	pkgdesc="View audio tag information from within Nemo's file properties tab"
	depends=('nemo-python' 'python2-mutagen')
	makedepends=('python2-distutils-extra')

	_setup.py
}

package_nemo-emblems() {
	pkgdesc="Change your folder and file emblems in Nemo"
	depends=('nemo-python')
	makedepends=('python2-distutils-extra')

	_setup.py
}

package_nemo-dropbox() {
	pkgdesc='Dropbox integration for Nemo'
	license=('CCPL' 'GPL')
	depends=('nemo' 'dropbox' 'libnotify')
	makedepends=('python2-docutils' 'pygtk' 'python2-gobject2')

	_make_install

	# remove dropbox executables since they'll be provided by the 'dropbox' package
	rm "${pkgdir}/usr/bin/dropbox"
	rm "${pkgdir}/usr/share/applications/dropbox.desktop"  
	rm "${pkgdir}/usr/share/man/man1/dropbox.1"
	rm -rf "${pkgdir}/usr/share/pixmap"
	rm -rf "${pkgdir}/usr/share/icons"
}

package_nemo-fileroller(){
	pkgdesc='File Roller integration for Nemo'
	depends=('nemo' 'file-roller')

	_make_install
}

package_nemo-preview(){
	pkgdesc='Quick file previewer for Nemo'
	depends=('nemo' 'cjs' 'clutter-gtk' 'clutter-gst' 'libmusicbrainz5' 'evince'
			'gtksourceview3' 'webkit2gtk' 'gst-plugins-good' 'gst-libav')
	makedepends=('gnome-common')

	_make_install
}

package_nemo-python() {
	pkgdesc='Python2 binding for Nemo components'
	depends=('nemo' 'python2-gobject')
	makedepends=('gnome-common')

	_make_install
}

package_nemo-seahorse(){
	pkgdesc='OpenPGP extension for Nemo'
	depends=('nemo' 'libcryptui' 'gcr' 'libgnome-keyring')

	_make_install

	# Conflict with seahorse-nautilus
	cd "${pkgdir}/usr/share/applications"
	mv seahorse-pgp-encrypted.desktop nemo-seahorse-pgp-encrypted.desktop
	mv seahorse-pgp-signature.desktop nemo-seahorse-pgp-signature.desktop
	mv seahorse-pgp-keys.desktop nemo-seahorse-pgp-keys.desktop
	cd "${pkgdir}/usr/share/GConf/gsettings"
	mv org.gnome.seahorse.nautilus.convert org.nemo.seahorse.convert
	cd "${pkgdir}/usr/share/glib-2.0/schemas"
	mv org.gnome.seahorse.nautilus.gschema.xml org.nemo.seahorse.gschema.xml
	mv org.gnome.seahorse.nautilus.window.gschema.xml org.nemo.seahorse.window.gschema.xml

}

package_nemo-share(){
	pkgdesc='Samba extension for Nemo'
	depends=('nemo' 'samba')

	_make_install
}

# package_nemo-media-columns(){
# 	cd ${pkgbase}-${_pkgver}/nemo-media-columns

# 	pkgdesc=("A Nemo extension to display music/EXIF and PDF metadata info in the Nemo List View")
# 	arch=(any)

#   install -d ${pkgdir}/usr/share/nemo-python/extensions/
# 	install -Dm755 ${srcdir}/nemo-media-columns.py \
# 			${pkgdir}/usr/share/nemo-python/extensions/nemo-media-columns.py
# }

# package_nemo-pastebin(){
# 	cd ${pkgbase}-${_pkgver}/nemo-pastebin

# 	pkgdesc="Nemo extension to send files to a pastebin"
# 	arch=(any)
# 	depends=(nemo pastebinit nemo-python)
#   install=nemo-pastebin.install

# 	python2 ./setup.py install --prefix=/usr --root=${pkgdir} \
# 		--no-compile -O0
# }

# package_nemo-compare() {
#   cd ${pkgbase}-${_pkgver}/nemo-compare

#   pkgdesc="Context menu comparison extension for Nemo file manager"
#   depends=(python2 python2-xdg meld nemo-python)

#   install -d ${pkgdir}/usr/share/applications/
#   install -d ${pkgdir}/usr/share/nemo-compare/

#   install -Dm755 ${srcdir}/data/nemo-compare-preferences.desktop \
#                   /usr/share/applications/nemo-compare-preferences.desktop
#   install -Dm755 ${srcdir}/data/nemo-compare-notification \
#                   /usr/share/nemo-compare/nemo-compare-notification
#   install -Dm755 ${srcdir}/src/nemo-compare.py \
#                   /usr/share/nemo-compare/nemo-compare.py
#   install -Dm755 ${srcdir}/src/utils.py \
#                   /usr/share/nemo-compare/utils.py
#   install -Dm755 ${srcdir}/src/nemo-compare-preferences.py \
#                   /usr/share/nemo-compare/nemo-compare-preferences.py

# }


# package_nemo-rabbitvcs(){
# 	cd ${pkgbase}-${_pkgver}/nemo-rabbitvcs

#   depends=(nemo-python python2-dbus)

#   install -d ${pkgdir}/usr/share/nemo-python/extensions/
#   install -Dm755 ${srcdir}/RabbitVCS.py \
#       ${pkgdir}/usr/share/nemo-python/extensions/RabbitVCS.py
# }

 
