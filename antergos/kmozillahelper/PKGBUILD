# Maintainer: Antergos Developers <dev@antergos.com>

pkgname=kmozillahelper
_pkgver=0.6.5
pkgver=${_pkgver}
pkgrel=1
pkgdesc="Mozilla KDE integration by openSUSE."
url="https://github.com/openSUSE/kmozillahelper"
arch=("i686" "x86_64")
license=('MIT')
depends=("kdoctools" "ki18n" "knotifications" "kio")
makedepends=("cmake" "automoc4" "extra-cmake-modules" "git")
provides=("kmozillahelper-frameworks" "kmozillahelper-kf5")
conflicts=("kmozillahelper-frameworks" "kmozillahelper-kf5")
replaces=("kmozillahelper-frameworks" "kmozillahelper-kf5")
source=("git+https://github.com/openSUSE/kmozillahelper.git")
md5sums=('SKIP')

pkgver() {
	cd ${pkgname}
	printf '%s.r%s' "${pkgver}" "$(git rev-list --count HEAD)"
}

build() {
	mkdir -p "${srcdir}/${pkgname}-build"
	cd "${srcdir}/${pkgname}-build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_TESTING=OFF \
		-DCMAKE_BUILD_TYPE=Release \
		"${srcdir}/${pkgname}"
	make
}

package() {
	cd "${srcdir}/${pkgname}-build"
	make DESTDIR="${pkgdir}" install
}
