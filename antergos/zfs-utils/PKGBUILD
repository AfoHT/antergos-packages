# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname=zfs-utils
_pkgver=0.7.1
_kern=4128
pkgver="${_pkgver}.${_kern}"
pkgrel=1
pkgdesc="Kernel module support files for the Zettabyte File System."
url="http://zfsonlinux.org/"
license=('CDDL')
arch=("x86_64")
groups=("system")
depends=("spl=${pkgver}")
conflicts=("zfs-utils-dkms-git")
_commit=''
_tag='zfs-0.7.1'
source=("https://github.com/zfsonlinux/zfs/archive/${_tag}.zip"
	"zfs-utils.bash-completion-r1"
	"zfs-utils.initcpio.install"
	"zfs-utils.initcpio.hook")
md5sums=('1a2b2fe26c3b0b11fd76c9cfdf6b8da8'
         '9ddb0c8a94861f929d0fa741fdc49950'
         '110b48d872a7a7d2884496267f92418a'
         '62ba32c6853c315ff231c396cf8bfdbb')


build() {
	cd "${srcdir}/zfs-${_tag}"
	./autogen.sh
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--with-mounthelperdir=/usr/bin \
		--libdir=/usr/lib \
		--datadir=/usr/share \
		--includedir=/usr/include \
		--with-udevdir=/usr/lib/udev \
		--libexecdir=/usr/lib/zfs \
		--with-config=user
	make
}

package() {
	cd "${srcdir}/zfs-${_tag}"
	make DESTDIR="${pkgdir}" install

	# Remove uneeded files
	rm -r "${pkgdir}"/etc/init.d
	rm -r "${pkgdir}"/usr/lib/dracut

	install -D -m644 "${srcdir}"/zfs-utils.initcpio.hook "${pkgdir}"/usr/lib/initcpio/hooks/zfs
	install -D -m644 "${srcdir}"/zfs-utils.initcpio.install "${pkgdir}"/usr/lib/initcpio/install/zfs
	install -D -m644 "${srcdir}"/zfs-utils.bash-completion-r1 "${pkgdir}"/usr/share/bash-completion/completions/zfs
}
