# Maintainer: Antergos devs <devs@antergos.com>
# AUR Maintainer: Jameson Pugh <imntreal@gmail.com>

pkgbase=octopi
pkgname=('octopi')
pkgver=0.8.5
pkgrel=4
# This is the release package so the below _gitcommit variable should (usually) be commented out.
_gitcommit="c413caadd11883ffcebf13ce515fb17bc23ebd27"
arch=('x86_64')
url="http://octopiproject.wordpress.com"
license=('GPL2')
makedepends=('qt5-declarative' 'git' 'knotifications' 'alpm_octopi_utils' 'qtermwidget')
pkgdesc="A powerful Pacman frontend using Qt5 libs"
depends=('qt5-declarative' 'alpm_octopi_utils' 'qtermwidget' 'libnotify' 'knotifications' 'xterm')
optdepends=('kdesu: for KDE'
			'gksu: for XFCE, Gnome, LXDE, Cinnamon'
			'lxqt-sudo: for LXQT'
			'gnome-keyring: for password management'
			'gist: for SysInfo report'
			'pacaur: for AUR support'
			'yaourt: for AUR support'
			'pacmanlogviewer: to view pacman log files')
conflicts=('octopi-git')
if [ "${_gitcommit}" != "" ]; then
    source=("octopi-${pkgver}-${pkgrel}.tar.gz::https://github.com/aarnt/octopi/archive/${_gitcommit}.tar.gz")
else
    source=("https://github.com/aarnt/${pkgname}/archive/v${pkgver}.tar.gz")
fi
source+=(https://raw.githubusercontent.com/manjaro/packages-community/9b69b357916e85855dc55015661e18047395688b/octopi/qt511.patch)
sha256sums=('13400ad5235331c7add1b0fa10a164b9b12a92c23e13dddea976935a9a259c92'
            '2d36c7ccc0235361bf105903281d05fe883559b6b34e550620faf9466aad9877')

prepare() {
    if [ "${_gitcommit}" != "" ]; then
        cd "${srcdir}/${pkgbase}-${_gitcommit}"
    else
        cd "${srcdir}/${pkgbase}-${pkgver}"
    fi

	# sed version
    sed -i -e "s|0.9.0 (dev)|${pkgver}-${pkgrel}|g" src/strconstants.h
    # enable the kstatus switch, disable if you wish to build without Plasma/knotifications support
    sed -e "s|DEFINES += ALPM_BACKEND #KSTATUS|DEFINES += ALPM_BACKEND KSTATUS|" -i notifier/octopi-notifier/octopi-notifier.pro
    
    patch -p1 -i $srcdir/qt511.patch
}

build() {
    if [ "${_gitcommit}" != "" ]; then
        cd "${srcdir}/${pkgbase}-${_gitcommit}"
    else
        cd "${srcdir}/${pkgbase}-${pkgver}"
    fi

    msg "Building octopi..."
    qmake-qt5 octopi.pro
    make

    cd notifier/pacmanhelper
    qmake-qt5 QMAKE_CFLAGS_RELEASE="${CFLAGS}" QMAKE_CXXFLAGS_RELEASE="${CXXFLAGS}" QMAKE_LFLAGS_RELEASE="${LDFLAGS}" pacmanhelper.pro
    make
    
    cd ../../notifier/octopi-notifier
    qmake-qt5 QMAKE_CFLAGS_RELEASE="${CFLAGS}" QMAKE_CXXFLAGS_RELEASE="${CXXFLAGS}" QMAKE_LFLAGS_RELEASE="${LDFLAGS}" octopi-notifier.pro
    make
    
    cd ../../repoeditor
    qmake-qt5 QMAKE_CFLAGS_RELEASE="${CFLAGS}" QMAKE_CXXFLAGS_RELEASE="${CXXFLAGS}" QMAKE_LFLAGS_RELEASE="${LDFLAGS}" octopi-repoeditor.pro
    make

    cd ../cachecleaner
    qmake-qt5 QMAKE_CFLAGS_RELEASE="${CFLAGS}" QMAKE_CXXFLAGS_RELEASE="${CXXFLAGS}" QMAKE_LFLAGS_RELEASE="${LDFLAGS}" octopi-cachecleaner.pro
    make
}

package() {
    if [ "${_gitcommit}" != "" ]; then
        cd "${srcdir}/${pkgbase}-${_gitcommit}"
    else
        cd "${srcdir}/${pkgbase}-${pkgver}"
    fi

    install -D -m755 "bin/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
    install -D -m644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
    install -D -m644 "resources/images/${pkgname}_green.png" "${pkgdir}/usr/share/icons/${pkgname}.png"
    install -D -m644 "resources/images/${pkgname}_green.png" "${pkgdir}/usr/share/icons/gnome/32x32/apps/${pkgname}.png"
    install -D -m644 "resources/images/${pkgname}_red.png" "${pkgdir}/usr/share/icons/${pkgname}_red.png"
    install -D -m644 "resources/images/${pkgname}_yellow.png" "${pkgdir}/usr/share/icons/${pkgname}_yellow.png"

    #speedup files
    install -D -m755 "speedup/speedup-octopi.sh" "${pkgdir}/usr/bin/speedup-octopi.sh"
    install -D -m644 "speedup/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"

    # pcmanhelper
    install -Dm755 "notifier/bin/pacmanhelper" "${pkgdir}/usr/lib/octopi/pacmanhelper"
    install -Dm644 "notifier/pacmanhelper/polkit/org.octopi.pacman.policy" "${pkgdir}/usr/share/polkit-1/actions/org.octopi.pacman.policy"
    install -Dm644 "notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.conf" "${pkgdir}/etc/dbus-1/system.d/org.octopi.pacmanhelper.conf"
    install -Dm644 "notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.xml" "${pkgdir}/usr/share/dbus-1/interfaces/org.octopi.pacmanhelper.xml"
    install -Dm644 "notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.service" "${pkgdir}/usr/share/dbus-1/system-services/org.octopi.pacmanhelper.service"

    # notifier
    install -D -m755 "notifier/bin/octopi-notifier" "${pkgdir}/usr/bin/octopi-notifier"
    install -D -m644 "notifier/octopi-notifier/octopi-notifier.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
    install -D -m644 "notifier/octopi-notifier/octopi-notifier.desktop" "${pkgdir}/etc/xdg/autostart/${pkgname}.desktop"

    # repoeditor
    install -D -m755 "repoeditor/bin/octopi-repoeditor" "${pkgdir}/usr/bin/octopi-repoeditor"
    #install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
    #install -D -m644 "resources/images/${pkgbase}_red.png" "${pkgdir}/usr/share/icons/${pkgname}.png"

    # cachecleaner files
    install -D -m755 "cachecleaner/bin/octopi-cachecleaner" "${pkgdir}/usr/bin/octopi-cachecleaner"
    #install -D -m644 "cachecleaner/$pkgname.desktop" "${pkgdir}/usr/share/applications/$pkgname.desktop"
}
