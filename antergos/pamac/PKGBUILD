# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Guillaume Benoit <guillaume@manjaro.org>

pkgname=(pamac pamac-tray-appindicator)
pkgver=6.2.6
_pkgver=6.2.6
pkgrel=2
pkgdesc='A GTK3 frontend for libalpm'
arch=('x86_64')
url='https://github.com/manjaro/pamac'
license=('GPL3')
depends=(
	'glib2' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3' 'gtk3'
	'libnotify' 'desktop-file-utils' 'pacman>=5.0' 'pacman<5.1' 'gnutls'
	'appstream-glib' 'archlinux-appstream-data'
)
makedepends=('gettext' 'itstool' 'vala' 'git' 'libappindicator-gtk3' 'meson')
backup=('etc/pamac.conf')
conflicts=('pamac-dev')
source=(
	"pamac-$_pkgver.tar.gz::$url/archive/v$_pkgver.tar.gz"
	"git-$pkgver-$pkgrel.patch::https://github.com/manjaro/pamac/compare/v$_pkgver...afd343e.patch"
)
md5sums=('765c5b6ade6b5d3dc1f9141ae558ff99'
         '3cb45dff242c98f2c519d18e418192ce')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='manjaro'
_monitored_repo='pamac'
# END ANTBS METADATA


prepare() {
	cd "${srcdir}/pamac-${pkgver}"

	# adjust version string
	sed -i -e "s|\"$_pkgver\"|\"$pkgver-$pkgrel\"|g" src/manager_window.vala 

	# patches here
	patch -Np1 -i ../../"git-$pkgver-$pkgrel.patch"
}

build() {
	cd "${srcdir}/pamac-${pkgver}"
	mkdir -p build && cd build

	meson --prefix=/usr --sysconfdir=/etc --libdir=lib -Denable-appindicator=true ..
	ninja
}

package_pamac() {
	optdepends=(
		'pamac-dev-tray-appindicator: tray icon for KDE'
	)
	conflicts=('pamac')
	provides=('pamac')
	backup=('etc/pamac.conf')


	cd "${srcdir}/pamac-${pkgver}/build"
	DESTDIR="${pkgdir}" ninja install

	# Enable applicable systemd timers
	mkdir -p "${pkgdir}/etc/systemd/system/multi-user.target.wants"
	ln -sf /usr/lib/systemd/system/pamac-cleancache.timer "${pkgdir}/etc/systemd/system/multi-user.target.wants"

	# Remove pamac-tray-appindicator
	rm "${pkgdir}/usr/bin/pamac-tray-appindicator"
	rm -f "${pkgdir}/etc/xdg/autostart/pamac-tray-appindicator.desktop"
}

package_pamac-tray-appindicator() {
	pkgdesc='Tray icon using appindicator for improved KDE support.'
	depends=('pamac' 'libappindicator-gtk3')
	conflicts=('pamac-tray-appindicator-dev')
	backup=()

	cd "${srcdir}/pamac-${pkgver}/build"
	install -Dm755 src/pamac-tray-appindicator "${pkgdir}/usr/bin/pamac-tray-appindicator"
	install -Dm644 ../data/applications/pamac-tray-appindicator.desktop "${pkgdir}/etc/xdg/autostart/pamac-tray-appindicator.desktop"
}

