# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Guillaume Benoit <guillaume@manjaro.org>

pkgname=(pamac pamac-tray-appindicator)
pkgver=6.0.4
_pkgver=6.0.4
pkgrel=2
pkgdesc='A GTK3 frontend for libalpm'
arch=('i686' 'x86_64')
url='https://github.com/manjaro/pamac'
license=('GPL3')
depends=(
	'glib2' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3' 'gtk3'
	'libnotify' 'desktop-file-utils' 'pacman>=5.0' 'pacman<5.1' 'gnutls'
	'appstream-glib' 'archlinux-appstream-data'
)
makedepends=('gettext' 'itstool' 'vala' 'git' 'libappindicator-gtk3')
backup=('etc/pamac.conf')
conflicts=('pamac-dev')
source=(
	"pamac-$_pkgver.tar.gz::$url/archive/v$_pkgver.tar.gz"
)
md5sums=('98869851a3b7010ab45798a057953541')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='manjaro'
_monitored_repo='pamac'
# END ANTBS METADATA


prepare() {
	cd "${srcdir}/pamac-${pkgver}"

	# adjust version string
	sed -i -e "s|\"$_pkgver\"|\"$pkgver-$pkgrel\"|g" src/manager_window.vala 
	# don't install systemd timers for manjaro's pacman-mirrors script
	sed -i '/pamac-mirrorlist/d' Makefile

	# patches here
}

build() {
	cd "${srcdir}/pamac-${pkgver}"

	make all
}

package_pamac() {
	optdepends=(
		'polkit-gnome: needed for authentification in Cinnamon, Gnome'
		'lxsession: needed for authentification in Xfce, LXDE etc.'
		'pamac-dev-tray-appindicator: tray icon for KDE'
	)
	conflicts=('pamac')
	provides=('pamac')
	backup=('etc/pamac.conf')


	cd "${srcdir}/pamac-${pkgver}"
	make prefix="${pkgdir}/usr" sysconfdir="${pkgdir}/etc" install
}

package_pamac-tray-appindicator() {
	pkgdesc='Tray icon using appindicator for improved KDE support.'
	depends=('pamac' 'libappindicator-gtk3')
	conflicts=('pamac-tray-appindicator-dev')

	cd "${srcdir}/pamac-${pkgver}"
	make use_appindicator=true
	make prefix="${pkgdir}/usr" sysconfdir="${pkgdir}/etc" install_pamac-tray-appindicator
}

