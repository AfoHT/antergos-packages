# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Weng Xuetian <wengxt@gmail.com>
# Contributor: Thaodan <theodorstormgrade@gmail.com>

_pkgname=firefox
pkgname=firefox-kde
pkgver=49.0.1
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org with openSUSE KDE integration patches."
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
url="https://build.opensuse.org/package/show/mozilla:Factory/MozillaFirefox"
groups=('browser')
depends=('alsa-lib' 'dbus-glib' 'ffmpeg' 'gtk2' 'gtk3' 'hunspell' 'icu' 'libevent' 'libvpx'
		'libxt' 'mime-types' 'mozilla-common' 'nss' 'startup-notification' 'sqlite' 'ttf-font'
		'kmozillahelper')
makedepends=('unzip' 'zip' 'diffutils' 'python2' 'yasm' 'mesa' 'imake' 'gconf'
			'libpulse' 'inetutils' 'xorg-server-xvfb' 'autoconf2.13' 'rust')
optdepends=('networkmanager: Location detection via available WiFi networks'
			'upower: Battery API')
provides=("firefox=${pkgver}")
conflicts=('firefox')
options=('!emptydirs' '!makeflags')
_patchrev=9fc2ebe6d7f1
_patchurl=http://www.rosenauer.org/hg/mozilla/raw-file/${_patchrev}
source=("https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/source/${_pkgname}-${pkgver}.source.tar.xz"
		'mozconfig'
		'firefox.desktop'
		'firefox-install-dir.patch'
		'vendor.js'
		'kde.js'
		'firefox-symbolic.svg'
		# Firefox patchset
		$_patchurl/firefox-branded-icons.patch
		$_patchurl/firefox-kde.patch
		$_patchurl/firefox-no-default-ualocale.patch
		# Gecko/toolkit patchset
		$_patchurl/mozilla-kde.patch
		$_patchurl/mozilla-language.patch
		$_patchurl/mozilla-nongnome-proxies.patch
		$_patchurl/mozilla-reduce-files-per-UnifiedBindings.patch)
md5sums=('3ce01741b9eab6a2988b970127330dcd'
         '77334bc5b58c888e6c7db50718f11be1'
         '9b02198df96be08f2a0a323ac2e6c2a2'
         'dbf14588e85812ee769bd735823a0146'
         '0d053487907de4376d67d8f499c5502b'
         '75df0f88cc7a7fa7d522459e4ff82cc5'
         '961a929a85e02e00eb11e3e303a4bc0e'
         '2fdf03e2bcb442342928863dc9635789'
         '3b3eb8c2747429887e4a03537932eac5'
         'edb29c1ac737a2a2ecdfd499bd30237a'
         '1fad9a988826d69fe712ea973e43f6da'
         '7b8cf4883062938956f202d04af47411'
         '903307f923a459189a5a6062ff9df38c'
         '0c684360f1df4536512d51873c1d243d'
         'e4c3fa5aed9663316ea6cdb9c533242f'
         '4d240471a642270accd515199ca698e4')

# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Antergos Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact info@antergos.com for
# more information.
_google_apps_domain='.apps.googleusercontent.com'
_google_api_key='AIzaSyBPgpho0q0siP2tggJwIsDx8ykpp-Y76Xk'
_google_default_client_id="523794628393-5f327td2rntpgvfvitoe2l36b2k4qdmq${_google_apps_domain}"
_google_default_client_secret='PGTWmwZDJ5VjvFoCKfVc19cK'

# Mozilla API keys (see https://location.services.mozilla.com/api)
# Note: These are for Antergos Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact info@antergos.com for
# more information.
_mozilla_api_key='fa35d4d6-6529-436f-b3a1-8b9af187eca2'


prepare() {
	cd "${_pkgname}-${pkgver}"

	cp ../mozconfig .mozconfig
	patch -Np1 -i ../firefox-install-dir.patch

	patch -Np1 -i "${srcdir}/mozilla-nongnome-proxies.patch"
	patch -Np1 -i "${srcdir}/mozilla-kde.patch"
	patch -Np1 -i "${srcdir}/mozilla-language.patch"
	patch -Np1 -i "${srcdir}/firefox-kde.patch"
	patch -Np1 -i "${srcdir}/firefox-no-default-ualocale.patch"
	patch -Np1 -i "${srcdir}/firefox-branded-icons.patch"
	patch -Np1 -i "${srcdir}/mozilla-reduce-files-per-UnifiedBindings.patch"

	echo -n "$_google_api_key" >google-api-key
	echo "ac_add_options --with-google-api-keyfile=\"$PWD/google-api-key\"" >>.mozconfig

	echo -n "$_google_default_client_id $_google_default_client_secret" >google-oauth-api-key
	echo "ac_add_options --with-google-oauth-api-keyfile=\"$PWD/google-oauth-api-key\"" >>.mozconfig

	echo -n "$_mozilla_api_key" >mozilla-api-key
	echo "ac_add_options --with-mozilla-api-keyfile=\"$PWD/mozilla-api-key\"" >>.mozconfig

	[[ 'i686' = "${CARCH}" ]] && echo 'ac_add_options --target=i686-unknown-linux-gnu' >>.mozconfig

	mkdir "$srcdir/path"
	ln -s /usr/bin/python2 "$srcdir/path/python"
}


build() {
	cd "${_pkgname}-${pkgver}"

	# _FORTIFY_SOURCE causes configure failures
	CPPFLAGS+=" -O2"

	# Hardening
	LDFLAGS+=" -Wl,-z,now"

	# GCC 6
	CXXFLAGS+=" -fno-delete-null-pointer-checks -fno-schedule-insns2"

	export PATH="${srcdir}/path:$PATH"

	# Do PGO
	# xvfb-run -a -s "-extension GLX -screen 0 1280x1024x24" \
	# make -f client.mk build MOZ_PGO=1
	make -f client.mk build
}

package() {
	cd "${_pkgname}-${pkgver}"

	[[ "$CARCH" == "i686" ]] && cp "$srcdir/kde.js" obj-i686-unknown-linux-gnu/dist/bin/defaults/pref
	[[ "$CARCH" == "x86_64" ]] && cp "$srcdir/kde.js" obj-x86_64-pc-linux-gnu/dist/bin/defaults/pref

	make -f client.mk DESTDIR="${pkgdir}" INSTALL_SDK= install

	install -Dm644 ../vendor.js "${pkgdir}/usr/lib/${_pkgname}/browser/defaults/preferences/vendor.js"
	install -Dm644 ../kde.js "${pkgdir}/usr/lib/firefox/browser/defaults/preferences/kde.js"

	for i in 16 22 24 32 48 256; do
		install -Dm644 "browser/branding/official/default${i}.png" \
			"${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/${_pkgname}.png"
	done

	_branding='browser/branding/official'
	_hicolor="${pkgdir}/usr/share/icons/hicolor"
	install -Dm644 "${_branding}/content/icon64.png" "${_hicolor}/64x64/apps/${_pkgname}.png"
	install -Dm644 "${_branding}/mozicon128.png" "${_hicolor}/128x128/apps/${_pkgname}.png"
	install -Dm644 "${_branding}/content/about-logo.png" "${_hicolor}/192x192/apps/${_pkgname}.png"
	install -Dm644 "${_branding}/content/about-logo@2x.png" "${_hicolor}/384x384/apps/${_pkgname}.png"
	install -Dm644 ../firefox-symbolic.svg "${_hicolor}/symbolic/apps/${_pkgname}-symbolic.svg"

	install -Dm644 ../firefox.desktop "${pkgdir}/usr/share/applications/firefox.desktop"

	# Use system-provided dictionaries
	rm -rf "${pkgdir}/usr/lib/${pkgname}"/{dictionaries,hyphenation}
	ln -s /usr/share/hunspell "${pkgdir}/usr/lib/${_pkgname}/dictionaries"
	ln -s /usr/share/hyphen "${pkgdir}/usr/lib/${_pkgname}/hyphenation"

	# Replace duplicate binary with symlink
	# https://bugzilla.mozilla.org/show_bug.cgi?id=658850
	ln -sf firefox "${pkgdir}/usr/lib/${_pkgname}/firefox-bin"
}

# -*- mode: bash;-*-
