#
# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>
#

pkgname=zfs
pkgdesc="Kernel modules for the Zettabyte File System."
pkgver=0.6.5.3
_pkgver=0.6.5.3
pkgrel=1
license=('CDDL')
makedepends=("git" "linux-headers")
depends=("spl" "zfs-utils" "dkms" "linux-headers")
provides=("zfs" "zfs-dkms")
conflicts=("zfs-git" "zfs-lts" "zfs-dkms")
install=zfs.install
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=("zfs::git+http://github.com/zfsonlinux/zfs.git")
md5sums=('SKIP')

_kernel=4.2.5
_kernel=4.2.5

pkgver() {
    cd "${srcdir}/zfs"
    printf "%s.r%s_%s" "${_pkgver}" "$(git rev-list --count HEAD)" "${_kernel}"
}

build() {
    cd "${srcdir}/zfs"
    ./autogen.sh
    scripts/dkms.mkconf -v ${_pkgver} -f dkms.conf -n zfs
    
    _i686_target=""
    [[ "${CARCH}" == i686 ]] && _i686_target="--target=i686-linux-gnu"
    
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --bindir=/usr/bin \
                --sbindir=/usr/bin \
                --libdir=/usr/lib \
                --datadir=/usr/share \
                --includedir=/usr/include \
                --with-udevdir=/usr/lib/udev \
                --libexecdir=/usr/lib/zfs \
                --with-config=user \
                ${_i686_target}
    make
}

package() {
    install -d ${pkgdir}/usr/src
    cp -a ${srcdir}/zfs ${pkgdir}/usr/src/zfs-${_pkgver}
    rm -rf ${pkgdir}/usr/src/zfs-${_pkgver}/.git
    rm -f ${pkgdir}/usr/src/zfs-${_pkgver}/.gitignore
}

# -*- mode: bash;-*-
