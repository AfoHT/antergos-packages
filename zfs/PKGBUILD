#
# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>
#

pkgname=zfs
pkgdesc="Kernel modules for the Zettabyte File System."
pkgver=0.6.5.2
_pkgver=0.6.5.2
pkgrel=1
license=('CDDL')
makedepends=("git" "linux-headers")
depends=("spl-dkms=${_pkgver}-${pkgrel}" "zfs-utils=${_pkgver}-${pkgrel}" "dkms")
provides=("zfs")
conflicts=("zfs-git" "zfs-lts" "zfs-dkms")
install=zfs.install
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=("git+http://github.com/zfsonlinux/zfs.git"
        "zfs-utils.bash-completion-r1"
        "zfs-utils.initcpio.install"
        "zfs-utils.initcpio.hook")
md5sums=('SKIP'
         '9ddb0c8a94861f929d0fa741fdc49950'
         '9ef4841abb85dee611828cc7e6f5c6fe'
         '62ba32c6853c315ff231c396cf8bfdbb')

_kernel="$(uname -r)"
_kernel="${_kernel%-ARCH}"

pkgver() {
    cd "${srcdir}/zfs"
    printf "%s.r%s_%s" "${_pkgver}" "$(git rev-list --count HEAD)" "${_kernel}"
    
}

build() {
    cd "${srcdir}/zfs"
    ./autogen.sh
    scripts/dkms.mkconf -v ${_pkgver} -f dkms.conf -n zfs
    
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --bindir=/usr/bin \
                --sbindir=/usr/bin \
                --libdir=/usr/lib \
                --datadir=/usr/share \
                --includedir=/usr/include \
                --with-udevdir=/usr/lib/udev \
                --libexecdir=/usr/lib/zfs \
                --with-config=user
    make
}

package() {
    install -d ${pkgdir}/usr/src
    cp -a ${srcdir}/zfs ${pkgdir}/usr/src/zfs-${_pkgver}
    rm -rf ${pkgdir}/usr/src/zfs-${_pkgver}/.git
    rm -f ${pkgdir}/usr/src/zfs-${_pkgver}/.gitignore
}