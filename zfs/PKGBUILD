#
# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>
#

pkgname=zfs
pkgdesc="Kernel modules for the Zettabyte File System."
pkgver=0.6.5.4
_pkgver=0.6.5.4
pkgrel=3
license=('CDDL')
makedepends=("git" "linux-headers")
depends=("spl" "zfs-utils" "dkms" "linux-headers")
provides=("zfs-dkms")
conflicts=("zfs-git" "zfs-lts" "zfs-dkms")
install=zfs.install
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=("zfs::git+http://github.com/zfsonlinux/zfs.git")
md5sums=('SKIP')

_kernel=4.3.3

prepare() {
	sed -i "s|<PKGVER>|${_pkgver}|g" "${srcdir}/${pkgname}.install"
}

pkgver() {
    cd "${srcdir}/zfs"
    printf "%s.r%s_%s" "${_pkgver}" "$(git rev-list --count HEAD)" "${_kernel}"
}

build() {
    cd "${srcdir}/zfs"
    ./autogen.sh
    #scripts/dkms.mkconf -v ${_pkgver} -f dkms.conf -n zfs
    
    _i686_target=""
    [[ "${CARCH}" == i686 ]] && _i686_target="--target=i686-linux-gnu"
    
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --bindir=/usr/bin \
                --sbindir=/usr/bin \
                --libdir=/usr/lib \
                --datadir=/usr/share \
                --includedir=/usr/include \
                --with-udevdir=/usr/lib/udev \
                --libexecdir=/usr/lib/zfs \
                --with-config=user \
                ${_i686_target}
    make
}

package() {
    dkmsdir="${pkgdir}/usr/src/zfs-${_pkgver}"
    install -d "${dkmsdir}"

    cd "${srcdir}/zfs"
    git archive --format=tar HEAD | tar -x -C "${dkmsdir}"

    cd "${dkmsdir}"
    ./autogen.sh
    scripts/dkms.mkconf -v ${_pkgver} -f dkms.conf -n zfs
    chmod g-w,o-w -R .
}

# -*- mode: bash;-*-
