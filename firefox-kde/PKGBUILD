# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Weng Xuetian <wengxt@gmail.com>
# Contributor: Thaodan <theodorstormgrade@gmail.com>

# try to build with PGO
_pgo=true

pkgname=firefox-kde
pkgver=41.0.2
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org with openSUSE's KDE integratation patches."
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
url="https://build.opensuse.org/package/show/mozilla:Factory/MozillaFirefox"
depends=('gtk2' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types'
	   'dbus-glib' 'alsa-lib' 'desktop-file-utils' 'hicolor-icon-theme'
	   'libvpx' 'icu'  'libevent' 'nss>=3.18.1' 'nspr>=4.10.6' 'hunspell' 
	   'sqlite' 'libnotify')
makedepends=('unzip' 'zip' 'diffutils' 'python2' 'yasm' 'mesa' 'imake'
		 'xorg-server-xvfb' 'libpulse' 'gst-plugins-base-libs' 'inetutils')
optdepends=('networkmanager: Location detection via available WiFi networks'
		'gst-plugins-good: h.264 video'
		'gst-libav: h.264 video')
provides=("firefox=${pkgver}")
conflicts=('firefox')
install=firefox.install
_patchrev=7aa7715fdc8f
options=('!emptydirs'  'strip' )
_patchurl="http://www.rosenauer.org/hg/mozilla/raw-file/$_patchrev"
source=("https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/source/firefox-$pkgver.source.tar.xz"
	  'mozconfig' 'firefox.desktop' 'firefox-install-dir.patch' 'vendor.js' 'kde.js'
	  # Firefox patchset
	  "$_patchurl/firefox-branded-icons.patch"
	  "$_patchurl/firefox-kde.patch"
	  "$_patchurl/firefox-no-default-ualocale.patch"
	  # Gecko/toolkit patchset
	  "$_patchurl/mozilla-kde.patch"
	  "$_patchurl/mozilla-language.patch"
	  "$_patchurl/mozilla-nongnome-proxies.patch"
	  "$_patchurl/toolkit-download-folder.patch"
	  "add_missing_pgo_rule.patch"
	  "pgo_fix_missing_kdejs.patch")
md5sums=('d71f0f761c51aeae03e367001afc9f8d'
         '8041491a8ee98ee9288a5e207a855f90'
         '9b02198df96be08f2a0a323ac2e6c2a2'
         'dbf14588e85812ee769bd735823a0146'
         '0d053487907de4376d67d8f499c5502b'
         '05bb69d25fb3572c618e3adf1ee7b670'
         '6bf26e2c182dabcba78f08a84a37192c'
         'b552f17a73557ddf73f3c2daad62d292'
         '648958ae7db60bc16dca3f847ee9791b'
         'bf5a2aa6dbdd4141375ae450ba405e2b'
         '4b6c9c35110f5e59fc87e1c8f3c33001'
         '0c684360f1df4536512d51873c1d243d'
         'e52d3e079d1c47914c12d3fc25e30abd'
         'fe24f5ea463013bb7f1c12d12dce41b2'
         '76af2dc85e02f87dda354fd886a34a03')

# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Antergos Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact info@antergos.com for
# more information.
_google_api_key=AIzaSyBPgpho0q0siP2tggJwIsDx8ykpp-Y76Xk
_google_default_client_id=523794628393-5f327td2rntpgvfvitoe2l36b2k4qdmq.apps.googleusercontent.com
_google_default_client_secret=PGTWmwZDJ5VjvFoCKfVc19cK

# Mozilla API keys (see https://location.services.mozilla.com/api)
# Note: These are for Antergos Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact info@antergos.com for
# more information.
_mozilla_api_key=fa35d4d6-6529-436f-b3a1-8b9af187eca2



prepare() {
  cd mozilla-release

  cp "$srcdir/mozconfig" .mozconfig

  patch -Np1 -i "$srcdir/firefox-install-dir.patch"
  
  echo -n "$_google_api_key" >google-api-key
  echo "ac_add_options --with-google-api-keyfile=\"$PWD/google-api-key\"" >>.mozconfig

  echo -n "$_google_default_client_id $_google_default_client_secret" >google-oauth-api-key
  echo "ac_add_options --with-google-oauth-api-keyfile=\"$PWD/google-oauth-api-key\"" >>.mozconfig

  echo -n "$_mozilla_api_key" >mozilla-api-key
  echo "ac_add_options --with-mozilla-api-keyfile=\"$PWD/mozilla-api-key\"" >>.mozconfig

  patch -Np1 -i "$srcdir/toolkit-download-folder.patch"
  patch -Np1 -i "$srcdir/mozilla-nongnome-proxies.patch"
  patch -Np1 -i "$srcdir/mozilla-kde.patch"
  patch -Np1 -i "$srcdir/mozilla-language.patch"

  patch -Np1 -i "$srcdir/firefox-kde.patch"
  patch -Np1 -i "$srcdir/firefox-no-default-ualocale.patch"
  patch -Np1 -i "$srcdir/firefox-branded-icons.patch"

  # add missing rule for pgo builds
  patch -Np1 -i "$srcdir"/add_missing_pgo_rule.patch

  # add missing file Makefile for pgo builds
  patch -Np1 -i "$srcdir"/pgo_fix_missing_kdejs.patch
  
  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  sed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure

  # WebRTC build tries to execute "python" and expects Python 2
  mkdir -p "$srcdir/path"
  ln -sf /usr/bin/python2 "$srcdir/path/python"

}

build() {

  cd mozilla-release

  export PATH="$srcdir/path:$PATH"
  export PYTHON="/usr/bin/python2"

  if [[ -n $_pgo ]]; then
    # Do PGO
    xvfb-run -a -s "-extension GLX -screen 0 1280x1024x24" \
      make -f client.mk build MOZ_PGO=1
  else
    make -f client.mk build
  fi
  
}

package() {
  cd mozilla-release

  [[ "$CARCH" == "i686" ]] && cp "$srcdir/kde.js" obj-i686-pc-linux-gnu/dist/bin/defaults/pref
  [[ "$CARCH" == "x86_64" ]] && cp "$srcdir/kde.js" obj-x86_64-unknown-linux-gnu/dist/bin/defaults/pref

  make -f client.mk DESTDIR="$pkgdir" INSTALL_SDK= install

  install -Dm644 "$srcdir/vendor.js" "$pkgdir/usr/lib/firefox/browser/defaults/preferences/vendor.js"
  install -Dm644 "$srcdir/kde.js" "$pkgdir/usr/lib/firefox/browser/defaults/preferences/kde.js"

  for i in 16 22 24 32 48 256; do
      install -Dm644 browser/branding/official/default$i.png \
        "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
  done

  install -Dm644 browser/branding/official/content/icon64.png \
    "$pkgdir/usr/share/icons/hicolor/64x64/apps/firefox.png"
  install -Dm644 browser/branding/official/mozicon128.png \
    "$pkgdir/usr/share/icons/hicolor/128x128/apps/firefox.png"
  install -Dm644 browser/branding/official/content/about-logo.png \
    "$pkgdir/usr/share/icons/hicolor/192x192/apps/firefox.png"
  install -Dm644 browser/branding/official/content/about-logo@2x.png \
    "$pkgdir/usr/share/icons/hicolor/384x384/apps/firefox.png"

  install -Dm644 "$srcdir/firefox.desktop" "$pkgdir/usr/share/applications/firefox.desktop"

  # Use system-provided dictionaries
  rm -rf "$pkgdir"/usr/lib/firefox/{dictionaries,hyphenation}
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/firefox/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/firefox/hyphenation"

  #workaround for now
  #https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -sf firefox "$pkgdir/usr/lib/firefox/firefox-bin"
}
