# Maintainer: Antergos Devs <dev@antergos.com>

pkgname=numix-frost-themes
_pkgname=Numix-Frost
_light=Numix-Frost-Light
_branch=numix-frost-light
pkgver=3.6.4
pkgrel=1
pkgdesc='Official Numix GTK theme - Antergos Edition'
arch=('any')
license=('GPL3')
groups=('themes' 'themes::gtk')
url='http://numixproject.org/'
depends=('gtk-engine-murrine' 'gtk3>=3.18')
makedepends=('ruby-sass' 'npm' 'gulp')
replaces=('gtk-theme-numix-frost')
source=('git+https://github.com/Antergos/Numix-Frost.git')
md5sums=('SKIP')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='antergos'
_monitored_repo='numix-frost'
# END ANTBS METADATA


package() {

	# Numix-Frost
	cd "${srcdir}/${_pkgname}/src"
	git checkout "${pkgver}"
	git submodule update --init -- includes/cinnamon
	make
	make DESTDIR="${pkgdir}" install


	# Cinnamon Theme
	sed -i 's%Numix-Cinnamon%Numix-Frost%g' includes/cinnamon/Numix-Cinnamon/cinnamon/theme.json
	install -d "${pkgdir}/usr/share/themes/${_pkgname}/cinnamon"
	cd includes/cinnamon/Numix-Cinnamon
	scss --update --sourcemap=none cinnamon/sass:cinnamon
	rm -rf cinnamon/sass
	rm -f cinnamon/*.js
	cp -R --no-preserve=ownership cinnamon "$pkgdir/usr/share/themes/$_pkgname"


	# GNOME Shell Theme
	install -d "${pkgdir}/usr/share/themes/${_pkgname}/gnome-shell"
	cd "${srcdir}/${_pkgname}/src/gnome-shell"
	find . -type f -exec install -Dm644 '{}' "$pkgdir/usr/share/themes/$_pkgname/gnome-shell/{}" \;


	# Numix-Frost-Light
	cd "${srcdir}/${_pkgname}/src"
	git reset --hard origin/master
	git checkout "${pkgver}-light"
	make
	make DESTDIR="${pkgdir}" install

}

# -*- mode: bash;-*-
