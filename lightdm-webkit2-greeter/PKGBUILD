# Maintainer: Antergos Developers <dev@antergos.com>

pkgname=lightdm-webkit2-greeter
pkgver=0.2.3beta1
pkgrel=1
pkgdesc="A webkit2 greeter for LightDM"
arch=('i686' 'x86_64')
url="https://github.com/antergos/lightdm-webkit2-greeter"
license=('GPL3')
makedepends=('gnome-doc-utils' 'gobject-introspection' 'intltool' 'gnome-common')
depends=('lightdm' 'webkit2gtk>=2.10.2' 'webkit2gtk<2.12' 'gtk3>=3.18' 'gtk3<3.20')
provides=('lightdm-webkit2-greeter' 'lightdm-webkit-greeter' 'lightdm-webkit-theme-antergos=2.2.4')
conflicts=('lightdm-webkit-greeter' 'lightdm-webkit-theme-antergos')
replaces=('lightdm-webkit-greeter' 'lightdm-webkit-theme-antergos<2.2.4')
install=greeter.install
backup=("etc/lightdm/${pkgname}.conf")
source=("$pkgname-$pkgver.tar.gz::https://github.com/antergos/lightdm-webkit2-greeter/archive/${pkgver}.tar.gz")
sha256sums=('d2cb9159a434a0f9e4ccbce68aeecf2240c727a03ad98cd5440ed56ab7a26a1d')

build() {
	cd $srcdir/${pkgname}-${pkgver}
	LIBS+="-ljavascriptcoregtk-4.0" ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/lightdm
	make

}

package() {
	cd $srcdir/${pkgname}-${pkgver}
	make DESTDIR=$pkgdir install 
}

# -*- mode: bash;-*-
