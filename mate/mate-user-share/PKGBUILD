# Maintainer: Antergos Developers <dev@antergos.com>#Maintainer: Martin Wimpress <code@flexion.org>

pkgname=mate-user-share
pkgver=1.14.0
pkgrel=1
pkgdesc="User level public file sharing via WebDAV for MATE."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'libunique' 'mod_dnssd')
makedepends=('caja>=1.14' 'libcanberra' 'libnotify' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-extra')
conflicts=("${pkgname}-gtk3")
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('c792d84a5580b4f48d6abd0769bca6395e5cc368')
install=${pkgname}.install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	PYTHON=/usr/bin/python2 ./configure \
		--prefix=/usr \
		--libexec=/usr/lib/${pkgname} \
		--sysconfdir=/etc \
		--disable-static \
		--disable-bluetooth
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make  DESTDIR="${pkgdir}" install
	rm -f "${pkgdir}/usr/share/mate-user-share/dav_user_2.0.conf"
}
