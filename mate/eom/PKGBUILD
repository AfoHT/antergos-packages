# Maintainer: Antergos Developers <dev@antergos.com># $Id: PKGBUILD 166764 2016-03-14 21:26:19Z bgyorgy $

# Contributor: Martin Wimpress <code@flexion.org>

pkgname=eom
pkgver=1.14.1
pkgrel=1
pkgdesc="An image viewing and cataloging program for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'gobject-introspection-runtime'
		'gtk3' 'exempi' 'lcms2' 'libexif' 'libjpeg-turbo' 'librsvg'
		'mate-desktop>=1.14' 'pygtk' 'python-gobject2' 'python-gobject'
		'startup-notification' 'zlib')
makedepends=('gobject-introspection' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('3b94e003ad2d29dd04f79198f9e03d823c285d50')
install=${pkgname}.install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	PYTHON=/usr/bin/python2 ./configure \
		--prefix=/usr \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/${pkgname} \
		--localstatedir=/var \
		--with-gtk=3.0 \
		--with-librsvg \
		--enable-python
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
