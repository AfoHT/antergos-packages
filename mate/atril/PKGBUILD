# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=atril
pkgver=1.14.1
pkgrel=1
pkgdesc="MATE document viewer (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'dbus' 'desktop-file-utils' 'gtk2' 'libsecret' 'libsm' 'libspectre'
		 'mate-desktop>=1.14' 'mathjax' 'poppler-glib' 'webkitgtk2' 'zlib')
makedepends=('caja>=1.14' 'djvulibre' 'gobject-introspection' 'libgxps'
			 'mate-common' 'texlive-bin' 'yelp-tools')
optdepends=('caja: Document Properties extension'
			'djvulibre: DjVu support'
			'libgxps: XPS support'
			'texlive-bin: DVI support'
			'yelp: for reading MATE help documents')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('c8d9ecf779f398439d67899e31722dd15d8b3b85')
install=${_pkgname}.install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/${pkgname} \
		--with-gtk=2.0 \
		--enable-gtk-doc \
		--enable-djvu \
		--enable-dvi \
		--enable-epub \
		--enable-t1lib \
		--enable-pixbuf \
		--enable-comics \
		--enable-xps \
		--enable-introspection \
		--disable-static
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
