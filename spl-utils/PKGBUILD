#
# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>
#
pkgname="spl-utils"
pkgver=0.6.5.3
_pkgver=0.6.5.3
pkgrel=2
license=('CDDL')
pkgdesc="Kernel module support files for the Solaris Porting Layer."
depends=("dkms")
provides=("spl-utils" "spl-utils-dkms")
conflicts=("spl-utils-dkms" "spl-utils-lts" "spl-utils-git")
makedepends=("git")
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=("spl::git+https://github.com/zfsonlinux/spl.git")
groups=("system" "zfs")
md5sums=('SKIP')

_kernel=4.2.5
_kernel=4.2.5

pkgver() {
    cd "${srcdir}/spl"
    printf "%s.r%s_%s" "${_pkgver}" "$(git rev-list --count HEAD)" "${_kernel}"
    
}

build() {
    cd "${srcdir}/spl"
    ./autogen.sh
    
    _at_enable=""
    [ "${CARCH}" == "i686"  ] && _at_enable="--enable-atomic-spinlocks"

    ./configure --prefix=/usr \
                --libdir=/usr/lib \
                --sbindir=/usr/bin \
                --with-config=user \
                ${_at_enable}

    make
}

package() {

    cd "${srcdir}/spl"
    make DESTDIR="${pkgdir}" install
    #install -D -m644 "${srcdir}"/spl-utils.hostid "${pkgdir}"/etc/hostid
}

# -*- mode: bash;-*-
